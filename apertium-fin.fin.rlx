# omorfi.cg3
# Constraint grammar for Finnish
# GNU GPLv3
# Authors: Tommi A Pirinen
# but also remade based on Fred Karlsson's fincg:

##! A note on comments: ##! is used for testing:
##! euro sign for example of remove not applying
##! dollar sign for remove rule firing

##  This program is free software: you can redistribute it and/or modify
##  it under the terms of the GNU General Public License as published by
##  the Free Software Foundation, either version 3 of the License.
##
##  This program is distributed in the hope that it will be useful,
##  but WITHOUT ANY WARRANTY; without even the implied warranty of
##  MERCHANTABILITY or FITNess FOR A parTICULAR PURPOSE.  See the
##  GNU General Public License for more details.
##
##  You should have received a copy of the GNU General Public License
##  along with this program.  If not, see <http://www.gnu.org/licenses/>.

# {{{ options
STRICT-TAGS += n vblex vaux adj adv adp np pron punct punct cnjcoo cnjadv ij num
    det
    nom par acc gen ine ela ill ade abl all tra ins abe lat ess
    sg pl
    actv pasv pot imp cond pri past
    infa infe infma infminen
    pxsg1 pxsg2 pxsp3 pxpl1 pxpl2
    enc qst
    ord card
    neg conneg
    pp pprs agent
    p1 p2 p3
    pos cmp sup
    pers dem
    use_dialect use_archaic use_nonstd use_blacklist
    ;
OPTIONS += no-inline-sets ;

DELIMITERS = "<.>" "<!>" "<?>" ;

# }}}

SETS

# {{{ grammar groups
## Nominal poses
LIST NOMINAL = n adj pron np ;
## Clitic particle tags
LIST CLITIC = enc qst ;
## Conjunctions
LIST CONJUNCTION = cnjcoo cnjadv ;
## any vebs
LIST VERBAL = vblex vaux ;
## copula?
LIST COPULA = ("olla" vaux) ;
##
LIST VBLEX-PL3 = (vblex p3 pl) (vaux p3 pl) ;
## ad-ADJectives etc.
LIST ADA = adv ;
## AD words
LIST AD = adj adv adp ;
## comparatives and superlatives
LIST CMP-SUP = cmp sup ;
##
LIST POSS = pxsg1 pxsg2 pxsp3 pxpl1 pxpl2 ;
## agent participle
LIST AGENT-PCP = agent ;
## case subgroups
LIST LOCATIVE-IN = ine ade ;
LIST LOCATIVE-TO = ill all ;
LIST LOCATIVE-FROM = ela abl ;
LIST ILL/ALL/PAR = ill all par ;
LIST PERS-PRON-SG = (pers p1 sg) (pers p2 sg) (pers p3 sg) ;
# ordinals XXX: adj doesn't have ord yet...
LIST NUM-ORD = (num ord) ;
LIST ADJ-ORD = (adj) ;
# Infintive a
LIST INFA-SG = (infa sg) ;
LIST INFA-PL = (infa pl) ;
# adj cases (and other agreement stuffs)
LIST ADJ-PAR = (adj par) (det par) (pron dem par) (num par) ;
LIST ADJ-INE = (adj ine) (det ine) (pron dem ine) (num ine) ;
LIST ADJ-ELA = (adj ela) (det ela) (pron dem ela) (num ela) ;
LIST ADJ-ILL = (adj ill) (det ill) (pron dem ill) (num ill) ;
LIST ADJ-ADE = (adj ade) (det ade) (pron dem ade) (num ade) ;
LIST ADJ-ABL = (adj abl) (det abl) (pron dem abl) (num abl) ;
LIST ADJ-ALL = (adj all) (det all) (pron dem all) (num all) ;
LIST ADJ-TRA = (adj tra) (det tra) (pron dem tra) (num tra) ;
LIST ADJ-ABE = (adj abe) (det abe) (pron dem abe) (num abe) ;
LIST ADJ-ESS = (adj ess) (det ess) (pron dem ess) (num ess) ;
# inmpv sg2 is systematically indv pres sg3
LIST IMP-SG2 = (imp p2 sg) ;
LIST INDV-SG3 = (pri p3 sg) ;
# }}}
# {{{ sentences and clauses
LIST BEGINSENT = >>> ;
LIST ENDSENT = "<.>" "<!>" "<?>" ;
LIST CLB = "<,>" ;
# }}}
# {{{ word groups

## short words that many find problems with
LIST BADEVIL_WORDS =
    ("kui" n) ("tai" n) ("vai" n) ("pai" n)
    ("par" n) ("tule" n) ("kuvi" n) ("kos" n)
    ("mon" n) ("lev" n) ("ka" n)
    ("taka" n) ("edus" n) ("jotta" n)
    ("he" ij) ;

## propns that overlap commoner words
LIST RARER-NP =
    ("Vanha" np) ("Mei" np) ("Osa" np) ("Hanska" np)
    ("Osta" np) ("Leikkaa" np) ("Varo" np) ("Tarkki" np)
    ("Onkki" np) ("Ede" np) ("Mietti" np) ("Metsä" np)
    ("Puu" np) ("Koto" np) ("Mörä" np) ("Tuli" np)
    ("Hauska" np) ("Uutinen" np) ("Piha" np) ("Sakko" np)
    ("Ota" np) ("Leikkaa" np) ("Lähdet" np) ("Matka" np)
    ("Lähellä" np) ("Makkara" np) ("Maku" np) ("Suo" np)
    ("Kauha" np) ("Tietty" np) ("Muu" np) ("Sen" np)
    ("Erinomainen" np) ("Allekirjoittanut" np) ("Markkina" np)
    ("Turu" np) ("Muu" np) ("Alku" np) ("Lava" np)
    ("Tila" np) ("Pyhä" np) ("Suora" np) ("He" np)
    ("Sen" np) ("Ainoa" np) ("Viimeinen" np) ("Ole" np)
    ("Jo" np) ("Ole" np) ("Ihana" np) ("Tos" np)
    ("Mies" np) ("As" np) ("Asi" np) ("Viimeinen" np)
    ("Toivo" np) ("Pitkä" np) ("Viimeinen" np)
    ("Lusikka" np) ("Asema" np) ("Mies" np)
    ("Olin" np) ("Tieto" np) ("Erä" np) ("Jyrkänne" np)
    ("Kello" np) ("Kuppi" np) ("Mutta" np) ("Yhtiö" np)
    ("Ka" np) ("Kutsu" np) ("Teos" np) ("Teema" np)
    ("Puute" np) ("Soi" np) ("Hakkuu" np) ("Tu" np)
    ("Vä" np) ("Vähä" np) ("Karta" np) ("Silta" np)
    ("Onni" np) ("Joki" np) ("Muuri" np) ("Vuo" np)
    ("Vuoksi" np) ("Kiva" np) ("Miele" np)
    ("Maakunta" np) ("Pia" np) ("Per" np) ("Olle" np)
    ("Johan" np) ("Aino" np) ("Halunen" np) ("Eni" np)
    ("Seuranen" np) ;
## also some cases where a rare noun blocks commoner propn
## often languages, nationalities, tribes
LIST RARER-NOUN = ("missouri" n) ("tai" n) ("vai" n)
    ("muutti" n) ("kanna" n) ;
## adverbs that are rarer than adp or noun counterparts. also some
## questionable advs that should've been intjs
LIST RARER-ADV =
    ("kuin" adv) ("kun" adv) ("tai" adv) ("sen" adv)
    ("viisi" adv) ("kuuluu" adv) ("koska" adv) ("pitkää" adv) ;
## verbs that shadow some noun forms etc.
LIST RARER-VBLEX =
    ("juuria" vblex) ("eritä" vblex) ("niittää" vblex)
    ("nytkiä" vblex) ("siitä" vblex) ("siittää" vblex)
    ("suomia" vblex) ("suoria" vblex) ("taata" vblex)
    ("vastoa" vblex) ("oieta" vblex) ("niistää" vblex)
    ("vedottaa" vblex) ;
## adverbs that are much commoner than nominal counterparts
LIST DEFAULT-ADV =
    ("erikseen" adv) ("erilleen" adv) ("erillään" adv)
    ("lainkaan" adv) ("läpeensä" adv) ("kaikkiaan" adv)
    ("kumminkaan" adv) ("kumminkin" adv) ("naimisiin" adv)
    ("naimisissa" adv) ("jäljelle" adv) ("kerrassaan" adv)
    ("kotoisin" adv) ("kumminkin" adv) ("kumminkaan" adv)
    ("oikeastaan" adv) ("parhaimmillaan" adv) ("pelkästään" adv)
    ("tietenkin" adv) ("tietenkään" adv) ("toisinaan" adv)
    ("tosiaan" adv) ("usein" adv) ("vahnastaan" adv)
    ("vähintään" adv) ("yhteensä" adv) ("yhtään" adv)
    ("yksinään" adv) ("yksistään" adv) ("öisin" adv)
    ("varsin" adv) ;
## following adverbs are quite safe to select umless matching case is in
## ±1 tokens (depending if adj or n ambiguity)
## inessives np initial
LIST ADV-LIKE-ADJ-INE =
    ("yhdessä" adv) ("vinossa" adv) ("pystyssä" adv) ;
## illatives np initial
LIST ADV-LIKE-ADJ-ILL =
    ("suoraan" adv) ("yksiin" adv) ("myöhään" adv)
    ("turhaan" adv) ("varmaan" adv) ("täyteen" adv)
    ("pitkään" adv) ("pystyyn" adv) ;
LIST ADV-LIKE-ADJ-ALL =
    ("uudelleen" adv) ;
LIST ADV-LIKE-ADJ-ELA =
    ("uudestaan" adv) ;
LIST ADV-LIKE-ADJ-ADE =
    ("syvällä" adv) ("samalla" adv) ("pitkällä" adv) ;
LIST ADV-LIKE-ADJ-INS =
    ("myöhemmin" adv) ("nykyisin" adv) ("aiemmin" adv) ;
LIST ADV-LIKE-ADJ-GEN = ("vähän" adv) ("paljon" adv)
    ("vähemmän" adv) ("enemmän" adv) ;
## translatives np final
## instructives ... maybe
LIST ADV-LIKE-NOUN-TRA =
    ("tarpeeksi" adv) ("käsiksi" adv) ("mieliksi" adv) ("osaksi" adv)
    ("periksi" adv) ;
## instructives ... maybe
LIST ADV-LIKE-NOUN-INS =
    ("paikoin" adv) ("selin" adv) ("silloin" adv)
    ("suinkin" adv) ("suinkaan" adv) ("tuskin" adv)
    ("vihdoin" adv) ("puolin" adv) ("muutoin" adv) ;
## adessives
LIST ADV-LIKE-NOUN-ADE =
    ("todella" adv) ("tavallaan" adv) ("mielellään" adv)
    ("perillä" adv) ("suunnitteilla" adv) ("vireillä" adv)
    ("jäljellä" adv) ;
LIST ADV-LIKE-NOUN-ALL =
    ("perille" adv) ("suunnilleen" adv) ;
LIST ADV-LIKE-NOUN-ELA =
    ("suorastaan" adv) ;
LIST ADV-LIKE-NOUN-INE =
    ("jumissa" adv) ;
## adps that are rarer than adv or noun counterparts
## prefer adpS-WITH-CASE over RARER-ADP
LIST RARER-ADP =
    ("silloin" adp) ("vaikka" adp) ("aina" adp) ;
## Adpositions by their complement
## adpositions with partitive complement
LIST ADPS-WITH-PAR = ("ylös" adp) ("päin" adp) ("vastaan" adp)
    ("ilman" adp) ("kautta" adp) ("huolimatta" adp) ("asti" adp)
    ("sitten" adp) ("ennen" adp) ;
## adositions with genitive complement
LIST ADPS-WITH-GEN = ("takana" adp) ("mukaan" adp) ("vuoksi" adp)
    ("alla" adp) ("kanssa" adp) ("kautta" adp) ("puolesta" adp)
    ("läpi" adp) ("johdolla" adp) ("sisällä" adp) ("perusteella" adp)
    ("sijaan" adp) ("suhteen" adp) ("avulla" adp) ("jälkeen" adp)
    ("myötä" adp) ;
## adpositions with illative complement
LIST ADPS-WITH-ILL = ("nähden" adp) ;
## adpositions with illative complement
LIST ADPS-WITH-ELA = ("ulos" adp) ;
## Verbs with inf complements
## ... infa
LIST VBLEXS-WITH-INFA = ("alkaa" vaux) ("haluta" vaux) ("tahtoa" vaux)
    ("saattaa" vaux) ("aikoa" vaux) ("pitää" vaux) ;
## ... inf maan
LIST VBLEXS-WITH-INFMAAN = ("alkaa" vaux) ("päästä" vaux) ("ruveta" vaux)
    ("pystyä" vblex) ("joutua" vaux) ("tulla" vblex) ;
## nouns where plural is rare / theoretical / shadows more common stuff
LIST NOUNS-RARELY-PLURAL = ("kota" n pl) ("ikä" n pl)
    ("kirjo" n pl) ("lei" n pl) ("bi" n pl)
    ("noki" n pl) ("tau" n pl) ("show" n pl)
    ("häkä" n pl) ("kulku" n pl) ("koko" n pl) ;
## numerals that are used nouns too
LIST NUM-MULTIPLIER = ("puoli" num) ("sata" num) ("miljoona" num)
    ("pari" num) ;
## temporal adverbs that can be noun forms
LIST TIME-ADV = ("maanantaina" adv) ("tiistaina" adv) ("keskiviikkona" adv)
    ("torstaina" adv) ("perjantaina" adv) ("lauantaina" adv)
    ("sunnuntaina" adv) ;
## curse words that go after voi
LIST VITTUSAATANA = ("vittu" ij) ("saatana" ij)
    ("hitto" ij) ("perkele" ij) ("!" punct) ;
## the words where 2nd person singular possessives overlap with e.g. past
## 3rd person singular e.g. "voisi" = {your butter(s), he could},
## "tiesi" = {your roads, he knew} but also: "kuusi" = {your moon(s),
## six}
LIST SG2POSS-CONFUSION = ("kuu" pxsg2) ("voi" pxsg2) ("tie" pxsg2)
    ("tunti" pxsg2) ("lupa" pxsg2) ("rupi" pxsg2) ("hai" pxsg2)
    ("taju" pxsg2) ("pyy" pxsg2) ("kerä" pxsg2) ("vasta" pxsg2)
    ("pää" pxsg2) ("harja" pxsg2) ("vuo" pxsg2) ("pää" pxsg2)
    ("halu" pxsg2) ("tapa" pxsg2) ("tosi" pxsg2) ("pola" pxsg2 ill)
    ("poli" pxsg2 ill);
## }}}
## {{{ words
## "siitä" = {to conceive, from it}
LIST SIITTÄÄ-VBLEXS = ("siittää" vblex) ("siitä" vblex) ("si’itä" vblex) ;
LIST SIITÄ-DEM = ("se" pron) ("se" det) ;
## "sinun" = yours
LIST SINUN = ("sinä" sg gen) ;
## ja adv is spoken
LIST JA-ADV = ("ja" adv) ;
## van Houten, etc.
LIST VAN-NP = ("van" np) ;
## Helsingin = {Helsinki’s, Helsing’s}
LIST HELSING-NP = ("Helsing" np) ;
## spoken
LIST ETTÄ-IJ = ("että" ij) ;
## sekä - että is paired cconj, otherwise että sconj
LIST SEKÄ-CNJCOO = ("sekä" cnjcoo) ;
LIST ETTÄ-CNJCOO = ("että" cnjcoo) ;
## niin - kuin is paired cconj
LIST NIIN-CNJCOO = ("niin" cnjcoo) ;
LIST KUIN-CNJCOO = ("kuin" cnjcoo) ;
## niiden {niisi’s, theirs} (I dunno what niisi is)
LIST NIISI-NOUN = ("niisi" n) ;
## ensin {first, some odd verb that doesn't really go with 1sg}
LIST ENTÄÄ-VBLEX = ("entää" vblex) ;
LIST ENSIN-ADV = ("ensin" adv) ;
## nainen {female, I might marry/fuck}
LIST NAINEN-VBLEX = ("naida" vblex pot) ;
## vähän = a little either adv or adj gen
LIST VÄHÄN-ADV = ("vähän" adv) ;
LIST VÄHÄN-ADJ = ("vähä" adj gen) ;
## vaikka mitä
LIST VAIKKA-ADV = ("vaikka" adv) ;
LIST MIKÄ-PRON = ("mikä" pron) ;
## häntä {tail, him}
LIST HÄNTÄ-NOUN = ("häntä" n) ;
LIST HÄNTÄ-PRON = ("hän" pron par) ;
## erikseen { separately, with his rounds }
LIST ERIKSEEN-ADV = ("erikseen" adv) ;
LIST ERIKSEEN-NOUN = ("erä" n pl pxsp3) ;
## jälkeen { after, into trace }
LIST JÄLKEEN-NOUN = ("jälki" n ill) ;
LIST JÄLKEEN-AD = ("jälkeen" adv) ("jälkeen" adp) ;
# jäljelle { left, onto trace }
LIST JÄLJELLE-NOUN = ("jälki" n all) ;
LIST JÄLJELLE-ADV = ("jälkeen" adv) ;
## uusia = { new, to renew }
LIST UUSI-ADJ = ("uusi" adj) ;
LIST UUSI-VBLEX = ("uusia" vblex) ;
## Japan is some rarer propn according to ...
LIST JAPANI-NP = ("Japani" np) ;
LIST JAPAN-NP = ("Japan" np) ;
## ajan { I drive, time, during }
LIST AJAN-NOUN = ("aika" n gen) ;
LIST AJAN-AD = ("ajan" adv) ("ajan" adp) ;
## koko = whole
LIST KOKO-ADJ = ("koko" adj) ;
## uudestaan = {again, from their new}
LIST UUDESTAAN-ADJ = ("uusi" adj ela pxsp3) ;
## jäljelle = {left, to a trace}
LIST JÄLJELLE-AD = ("jäljelle" adv) ("jäljelle" adp) ;
## ollenkaan = {at all, while being}
LIST OLLENKAAN-ADV = ("ollenkaan" adv) ;
LIST OLLENKAAN-VBLEX = ("olla" infe) ;
## erään = {of some, to a set}
LIST ERÄÄN-PRON = ("eräs" pron sg gen) ;
## merkitystä = {of importance, from marked}
LIST MERKITYSTÄ-par = ("merkitys" par) ;
## noissa = {in those, in ashes}
LIST NOISSA-PRON = ("nuo" pron ine) ("tuo" pron pl ine) ;
LIST NOISSA-NOUN = ("noki" sg ine) ;
## peräisin = {originating from, I'd guarantee}
LIST PERÄISIN-ADV = ("peräisin" adv) ;
LIST PERÄISIN-VBLEX = ("perätä" vblex cond p1 sg) ;
## sekin = {that one too, of cheque}
LIST SEKIN-PRON = ("se" pron) ;
LIST LUNASTAA-VBLEX = ("lunastaa" vblex) ;
##
LIST TUO-PRON = ("tuo" pron) ;
LIST KAIKKI = ("kaikki" pron) ("kaikki" adj) ;
## vastaan {against}
LIST VASTAAN-ADV = ("vastaan" adv) ;
##
LIST ARVOineN-ADJ = ("arvoinen" adj) ;
LIST KallIS-ADJ = ("kallis" adj) ;
## pieniä = {make smaller, small}
LIST PIENIÄ-ADJ = ("pieni" adj par) ;
## vaaleissa etc. {elections, blondes}
LIST VAALEISSA-ADJ = ("vaalea" adj pl ine) ;
LIST VAALEISTA-ADJ = ("vaalea" adj pl ela) ;
LIST VAALEIHIN-ADJ = ("vaalea" adj pl ill) ;
LIST VAALEillA-ADJ = ("vaalea" adj pl ade) ;
##
LIST OTTAA-VBLEX = ("ottaa" vblex) ;
##
LIST KallIS-NOUN = ("kallis" n) ;
##
LIST HYVIN-ADA = ("hyvin" adv) ;
LIST LIIAN-ADV = ("liian" adv) ;
##
LIST MITÄ-ADV = ("mitä" adv) ;
##
LIST TÄYSIN-ADA = ("täysin" adv) ;
##
LIST EDELLÄ-ADV = ("edellä" adv) ;
##
LIST ENNEN-ADV = ("ennen" adv) ;
##
LIST IKINÄ-ADV = ("ikinä" adv) ;
##
LIST KAHDEN-ADV = ("kahden" adv) ;
##
LIST KOOLLE-ADV = ("koolle" adv) ;
##
LIST KAUPPA-NOUN = ("kauppa" n) ;
##
LIST KOHTA-ADV = ("kohta" adv) ;
##
LIST KUTSUA-VBLEX = ("kutsua" vblex) ;
##
LIST LISÄÄ-ADV = ("lisää" adv) ;
##
LIST JOS-ADV = ("jos" adv) ;
##
LIST KAUEMMAKSI-ADV = ("kauemmaksi" adv) ;
LIST KAUEMPAA-ADV = ("kauempaa" adv) ;
LIST KAUEMPANA-ADV = ("kauempana" adv) ;
LIST KAUEMMAKSI-ADJ = ("kauempi" adj tra) ;
LIST KAUEMPAA-ADJ = ("kauempi" adj tra) ;
LIST KAUEMPANA-ADJ = ("kauempi" adj ess) ;
LIST LÄHEMMÄKSI-ADV = ("lähemmäksi" adv) ;
LIST LÄHEMPÄÄ-ADV = ("lähempää" adv) ;
LIST LÄHEMPÄNÄ-ADV = ("lähempänä" adv) ;
LIST LÄHEMMÄKSI-ADJ = ("lähempi" adj tra) ;
LIST LÄHEMPÄÄ-ADJ = ("lähempi" adj par) ;
LIST LÄHEMPÄNÄ-ADJ = ("lähempi" adj ess) ;
## näin = {so, I saw}
LIST NÄIN-ADV = ("näin" adv) ;
LIST NÄIN-VBLEX = ("nähdä" vblex p1 sg past) ;
##
LIST NIIN-ADV = ("niin" adv) ;
##
LIST ESIM = ("esim." n) ("esimerkiks" adv) ("esimerkki" n sg tra) ;
##
LIST TAI-CCONJ = ("tai" cnjcoo) ;
##
LIST NÄKÖJÄÄN-ADV = ("näköjään" adv) ;
##
LIST SIKSI-ADV = ("siksi" adv) ;
##
LIST SUOTTA-ADV = ("suotta" adv) ;
##
LIST SYNTYISIN-ADV = ("syntyisin" adv) ;
##
LIST TAKAISIN-ADV = ("takaisin" adv) ;
##
LIST TOISTAMISEEN-ADV = ("toistamiseen" adv) ;
##
LIST MINÄ-PRON = ("minä" pron) ;
##
LIST HEIDÄN-PRON = ("he" pron gen) ("hän" pron pl gen) ;
##
LIST LUJAA-ADV = ("lujaa" adv) ;
##
LIST SUORAAN-ADV = ("suoraan" adv) ;
##
LIST KAPINA-NOUNom = ("kapina" n nom) ;
## kotia = {home, lappic tent}
LIST KOTA-NOUN = ("kota" n) ;
LIST KOTI-NOUN = ("koti" n) ;
## kuolema = {death, die-er}
LIST KUOLEMA-VBLEX = ("kuolla" vblex agent) ;
LIST KUOLEMA-NOUN = ("kuolema" n) ;
## olkaa = {shoulder, be imperative (be quiet!)}
LIST OLKAA-NOUN = ("olka" n sg par) ;
LIST OLKAA-VBLEX = ("olla" vblex imp p2 pl) ;
## teillä = {teas, roads, yours}
LIST TEE-PLURAL = ("tee" n pl) ;
LIST TIE-PLURAL = ("tie" n pl) ;
LIST TE-PLURAL = ("te" pron) ("sinä" pron pl) ;
## tilanne = {your space, situation}
LIST TILANNE-NOUN = ("tilanne" n) ;
LIST TILANNE-POSS = ("tila" n pxpl2) ;
## vanhempi = {elder (parent), older}
LIST VANHEMPI-NOUN = ("vanhempi" n) ;
## hiukan = [a little, a dustknot's}
LIST HIUKAN-NOUN = ("hiukka" n sg gen) ;
## mm = {millimetres, world championship, 2000, among others, a-ha}
LIST MM-NOUN = ("mm" n) ;
## lähtien = {from onwards, by going}
LIST LÄHTIEN-ADP = ("lähtien" adp) ;
##
LIST PÄIN-ADP = ("päin" adp) ;
## omaa = {own verb / det}
LIST OMAAN-VBLEX = ("omata" vblex p1 sg) ;
LIST OMAAN-ADJ = ("oma" adj ill) ;
LIST OMAA-VBLEX = ("omata" vblex p3 sg) ;
LIST OMAA-ADJ = ("oma" adj par) ;
## voi = {butter, oh!, can, could}
LIST VOI-INTJ = ("voi" ij) ;
LIST VOI-VAUX = ("voida" vaux p3 sg) ;
## ei = {no, not, none, negative aux, }
LIST EI-INTJ = ("ei" ij) ;
LIST EI-VAUX = ("ei" vaux) ;
## tutlittava
LIST TUTKITTAVA-DRV-VA = ("tutkittaa" vblex) ;
LIST TUTKITTAVA-NOUN = ("tutkittava" n) ;
## merkittävä
LIST MERKITTÄVÄ-DRV-VA = ("merkittää" vblex) ;
LIST MERKITTÄVÄ-ADJ = ("merkittävä" adj) ;
## copulas for minen verb chains
LIST OLE-CONNEG = ("olla" vblex conneg) ;
## vaan = {not ... but (cnjcoo), not really at all (adv)}
LIST VAAN-ADV = ("vaan" adv) ;
LIST VAAN-CCONJ = ("vaan" cnjcoo) ;
## pidetään = {are held, are lengthened}
LIST PIDETÄ-PASS = ("pidetä" vblex pasv) ;
LIST PITÄÄ-PASS = ("pitää" vblex pasv) ;
## joissa = {in the rivers, which}
LIST JOISSA-PRON = ("joka" pron pl) ;
LIST JOISSA-NOUN = ("joki" n pl) ;
## apu, avu
LIST AVU-NOUN = ("apu" n) ;
LIST APU-NOUN = ("avu" n) ;
# }}}
# {{{ secondaries
## we have these tags when analyser doesn't match the case
LIST BLACKLISTED = use_blacklist ;
## tags for known wrong or odd stuff
LIST NONSTANDARDS = use_archaic use_dialect use_nonstd ;

# }}}

BEFORE-SECTIONS # {{{ preprocess worst stuff away and select options

## removes all analyses that get too many complaints by users
REMOVE:bad-words BADEVIL_WORDS ;

## ensure ordinals only gives one analysis
## (two is needed for different standards / treebanks)
REMOVE:UD-ordinal-is-ADJective NUM-ORD (0 ADJ-ORD) ;

## ensure infa short form only gives one analysis
## (two is needed for different standards / treebanks)
REMOVE:infa-is-sg INFA-PL (0 INFA-SG) ;
# }}}

SECTION # {{{ words minimal context

# trad. possessives
# sinun {kuusi, voisi, tiesi}
REMOVE:only-poss-if-PRON-sg2 SG2POSS-CONFUSION (NOT -1 SINUN) ;

# surface-form specific disambiguation between few lexemes

##! € ollaan menos sinne ja.
REMOVE:ja-ADV JA-ADV (NOT 1 ENDSENT) ;

##! € ai että, voi että.
REMOVE:että-intj ETTÄ-IJ (NOT -1 ij) ;
##! € oli siellä sekä kaljaa että apinoita.
REMOVE:sekä-että-cconj ETTÄ-CNJCOO (NOT -1* SEKÄ-CNJCOO) ;
##! € oli siellä niin kaljaa kuin apinoitakin.
REMOVE:niin-kuin-cconj NIIN-CNJCOO (NOT 1* KUIN-CNJCOO) ;

##! $ Helsingissä on lunta.
##! € näitkö sen uuden van Helsingin episodin?
REMOVE:Helsing-needs-van HELSING-NP (NOT -1 VAN-NP) ;

##! $ pulloissa on vanhan näköiset etiketit.
REMOVE:van-needs-NP VAN-NP (NOT 1 np) ;

##! $ Japaniin suuntautuva vierailu.
REMOVE:Japan-NOUNot-country JAPAN-NP (0 JAPANI-NP) ;

##! $ nainen on kuin morsian.
REMOVE:nainen-unlikely-potential  NAINEN-VBLEX (0 n) ;

##! € kun rahaa ei ole, löytää vaikka mitä.
##! $ vaikka rahaa ei ole, löytää sitä.
REMOVE:vaikka-mitä VAIKKA-ADV (NEGATE 1 MIKÄ-PRON) ;

##! $ niiden pitää tulla myös.
REMOVE:niisi-NOUNiiden NIISI-NOUN (0 pron) ;

##! $ siitä en tiedä mitään.
##! € siitä maailma täyteen lapsia.
REMOVE:siittää-siitä SIITTÄÄ-VBLEXS (0 SIITÄ-DEM) ;

##! $ ensin syödään sitten vasta mennään.
REMOVE:entää-ensin ENTÄÄ-VBLEX (0 ENSIN-ADV) ;


##! $ he odottivat häntä.
##! € koira juoksi häntä heiluen.
SELECT:häntä-PRON HÄNTÄ-PRON (0 HÄNTÄ-NOUN) ;

##! $ kaupunginvaltuusto hyväksyi liitoksen maanantaina.
SELECT:temprals-prefer-ADV TIME-ADV (0 n) ;

##! $ päätetään siitä erikseen.
SELECT:erikseen-ADV ERIKSEEN-ADV (0 ERIKSEEN-NOUN);

##! $ aamun jälkeen on yö.
REMOVE:jälkeen-ad JÄLKEEN-NOUN (0 JÄLKEEN-AD) ;
REMOVE:jäljelle-ADV JÄLJELLE-NOUN (0 JÄLJELLE-AD) ;

##! $ Intian uusi pääministeri on Narendra Modi.
SELECT:uusi-ADJ UUSI-ADJ (0 UUSI-VBLEX) ;

##! $ hän ei ole ollenkaan rehellinen.
SELECT:ollenkaan-ADV OLLENKAAN-ADV (0 OLLENKAAN-VBLEX) ;

##! $ oikeus omaan kehoon
##! $ minä omaan kehoni
SELECT:omaan-ADJ-ill OMAAN-ADJ (1 ill) ;
##! $ pitää perustella omaa olemassaoloaan
SELECT:omaa-ADJ-intj OMAA-ADJ (1 par) ;
##! $ voi vittu saatana!
REMOVE:voi-intj-intj VOI-INTJ (NEGATE 1 VITTUSAATANA) ;
##! $ lääkäri voi määrätä lääkkeitä
SELECT:voi-aux-infa VOI-VAUX (1* infa BARRIER vblex) ;
##! $ ei vittu saatana!
REMOVE:ei-intj-intj EI-INTJ (NEGATE 1 VITTUSAATANA) ;
##! $ tutkittavan todellisuudentaju näyttäytyy
REMOVE:tutkittava-over-tutkittaa TUTKITTAVA-DRV-VA (0 TUTKITTAVA-NOUN) ;
##! $ aiheesta annettiin ensimmäinen merkittävä päätös
REMOVE:merkittävä-over-merkittää MERKITTÄVÄ-DRV-VA (0 MERKITTÄVÄ-ADJ) ;
##! $ se ei vaan onnistu
SELECT:ei-vaan-ADV VAAN-ADV (-1 neg) ;
##! $ ulkopolitiikka ei tehdä pressan johdolla, vaan pressasta huolimatta
SELECT:ei-blah-vaan-cconj VAAN-CCONJ (-2* neg BARRIER BEGINSENT) ;
# }}}
SECTION # {{{ FGK-word-specific
# I have only selected the ones I can explain and fidn examples of:
# I also skipped:
# * adps that already have case bindings
# * and advs too
# * nouns that have generic poss solution (kuusi, voisi, ...)
# ––Flammie

##! $ emme ratkaise pitkän ajan ongelmia.
SELECT:ajan-noun AJAN-NOUN (-1 gen) ;
##! $ auringon pinta muuttuu koko ajan.
SELECT:ajan-noun AJAN-NOUN (-1 KOKO-ADJ) ;

##! $ erään sateen jälkeen olin yksin.
##! € toiseen erään vaihtopelaajien määrää vähennettiin.
SELECT:erään-PRON ERÄÄN-PRON (1 gen) ;




##! $ sillä on merkitystä elinkeinoelämässä.
##! € salasanan löydät seinälle merkitystä tekstistä.
SELECT:merkitystä-par MERKITYSTÄ-par (NOT 1 ela) ;

##! $ aiotko mennä sinne noissa vaatteissa.
SELECT:noissa-PRON NOISSA-PRON (0 NOISSA-NOUN) ;

##! $ mistä se on peräisin.
SELECT:peräisin-ADV PERÄISIN-ADV (0 PERÄISIN-VBLEX) (0* COPULA) ;

##! $ sekin on vielä.
##! € lunastan sen sekin jahka joudan.
SELECT:sekin-PRON SEKIN-PRON (NEGATE -1* LUNASTAA-VBLEX BARRIER vblex) ;


##! hmm?
SELECT:kaikki-tuo TUO-PRON (-1 KAIKKI) (NOT -2 pron) ;

##! $ tankki täyteen bensaa.

##! $ ota vastaan se lahja.
SELECT:ottaa-vastaan-ADV VASTAAN-ADV (-1* OTTAA-VBLEX BARRIER vblex) ;

##! $ minkä arvoinen turkis se oli.
SELECT:gen-arvoinen ARVOineN-ADJ (-1 gen) ;

##! $ Japani on kallis maa.
SELECT:kallis-ADJ KallIS-ADJ (0 KallIS-NOUN) ;


##! $ pieniä sieviä sieniä.
SELECT:pieniä-ADJ PIENIÄ-ADJ (1 par) ;


##! € presidentti Trump voitti vaaleissa.
##! $ vaaleissa munkeissa on enemmän makua.
REMOVE:vaaleissa-ADJ VAALEISSA-ADJ (NEGATE 1 ine) ;

##! $ pidän vaaleista naisista.
REMOVE:vaaleista-ADJ VAALEISTA-ADJ (NEGATE 1 ela) ;

##! € presidentti Trump toi vaaleihin markkinoiden tuntua.
REMOVE:vaaleihin-ADJ VAALEIHIN-ADJ (NEGATE 1 ill) ;

##! € presidentti Trump valittiin demokraattisilla vaaleilla.
REMOVE:vaaleilla-ADJ VAALEillA-ADJ (NEGATE 1 ade) ;

##! € se on hyvin suuri.
##! € se meni hyvin nopeasti.
SELECT:hyvin-ADV HYVIN-ADA (1 AD) ;

##! € se on liian vanha.
##! $ vim poistaa liian lian.
SELECT:liian-ADV LIIAN-ADV (NEGATE 1 gen) ;

##! € mitä suurempi peukalo sitä suurempi pikkurilli.
##! € mitä suurimmissa määrin kyllä.
SELECT:mitä-ada MITÄ-ADV (1 CMP-SUP) ;

##! $ hän on täysin kyvytön.
SELECT:täysin-ada TÄYSIN-ADA (1 AD) ;


##! $ he ovat vielä edellä.
##! € kuningas ei edellä jätkää.
##! € kuka on sinua edellä?
SELECT:edellä-ADV EDELLÄ-ADV (NEGATE -1* neg) (NEGATE -1 gen) (negATE -1 par) (negATE 1 par) ;

##! $
SELECT:ennen-ADV-kuin ENNEN-ADV (1 KUIN-CNJCOO) ;

##

##! € en olis ikinä uskonu.
SELECT:ikinä-ADV IKINÄ-ADV (NEGATE -1 ess) ;

##! € miten paljon aikaa on jäljellä.

##!
SELECT:kahden-ADV KAHDEN-ADV (NEGATE 1 gen) (NEGATE 1 KAUPPA-NOUN) ;



##!
SELECT:jos-kohta-ADV KOHTA-ADV (-1 JOS-ADV) ;

##! kutsutaan kokous koolle.
SELECT:kutsua-koolle KOOLLE-ADV (-1* KUTSUA-VBLEX) ;


##

SELECT:lisää-ADV LISÄÄ-ADV (0* par BARRIER CLB) ;

##!
SELECT:kauemmaksi-ADV KAUEMMAKSI-ADV (0 KAUEMMAKSI-ADJ) (NEGATE 1 tra) ;
SELECT:kauempaa-ADV KAUEMPAA-ADV (0 KAUEMPAA-ADJ) (NEGATE -1 par) ;
SELECT:kauempana-ADV KAUEMPANA-ADV (0 KAUEMPANA-ADJ) (NEGATE -1 ess) ;
SELECT:lähemmäksi-ADV LÄHEMMÄKSI-ADV (0 LÄHEMMÄKSI-ADJ) (NEGATE 1 tra) ;
SELECT:lähempää-ADV LÄHEMPÄÄ-ADV (0 LÄHEMPÄÄ-ADJ) (NEGATE 1 par) ;
SELECT:lähempänä-ADV LÄHEMPÄNÄ-ADV (0 LÄHEMPÄNÄ-ADJ) (NEGATE 1 ess) ;






SELECT:niin-tai-NOUNäin-ADV NÄIN-ADV (-2 NIIN-ADV) (-1 TAI-CCONJ) ;
SELECT:esim-NOUNäin-ADV NÄIN-ADV (-1 ESIM) ;

SELECT:näköjään-ADV NÄKÖJÄÄN-ADV (NEGATE -1 HEIDÄN-PRON) ;












##! € siksi olen niin iloinen.
##! € voin tulla siksi miksi minua luulet.
SELECT:siksi-ADV SIKSI-ADV (-1 BEGINSENT) (NEGATE 1 tra) ;





SELECT:suotta-ADV SUOTTA-ADV (NEGATE -1 abe) ;



SELECT:syntyisin-ADV SYNTYISIN-ADV (NEGATE -1 MINÄ-PRON) ;


SELECT:takaisin-ADV TAKAISIN-ADV (NEGATE -1 MINÄ-PRON) ;


SELECT:toistamiseen-ADV TOISTAMISEEN-ADV (NEGATE -1 gen) ;










##! $ he joivat vähän vettä.
##! € hän joi sen vähän veden mitä sai.
SELECT:vähän-ADV VÄHÄN-ADV (NOT 1 gen) ;




SELECT:lujaa-ADV LUJAA-ADV (NEGATE 1 par) ;


# suoraan = {directly, direct, straight (e.g. poker)}
SELECT:suoraan-ADV SUORAAN-ADV (NEGATE -1 ill) (NEGATE 1 ill) ;

## !
SELECT:kapina-NOUNom KAPINA-NOUNom (NEGATE -1 ess) ;

##! $ uudessa kodissa on mukava asua
##! € kai jossain lapissa asutaan vielä kodissa
REMOVE:koti-over-kota KOTA-NOUN (0 KOTI-NOUN) ;

##! $ kuolema korjaa satoa
REMOVE:kuolema-over-kuolla KUOLEMA-VBLEX (0 KUOLEMA-NOUN) ;

##! $ olkaa niin hyvä ja menkää.
##! $ jokaisella on kaksi olkaa.
REMOVE:olkaa-usually-verb OLKAA-NOUN (0 OLKAA-VBLEX) ;

##! $ teiden päässä on Rooma.
##! € Sri Lanka on teiden valmistajamaa.
REMOVE:tee-plural-shadows-tie TEE-PLURAL (0 TIE-PLURAL) ;
##! $ teissä on sitä jotain.
##! $ monissa teissä on paljon kofeiinia.
REMOVE:tee-plural-shadows-te TEE-PLURAL (0 TE-PLURAL) ;

##! $ tilanne oli kuumottava.
##! € työntekoon soveltuvat tilanne ovat olemattomat.
REMOVE:tilanne-over-tila TILANNE-POSS (0 TILANNE-NOUN) ;

##! € vanhemmat ovat vanhempainillassa.
##! € vanhepi herrasmies.
SELECT:vahnempi-noun VANHEMPI-NOUN (NEGATE 1 n) (NEGATE 2 n) ;

##! € hiukan malttia on hyvä olla.
REMOVE:hiukan-ADV HIUKAN-NOUN (0 adv) ;

##! € jana on 25 mm pitkä.
SELECT:mm-measure-with-nums MM-NOUN (-1 num) ;

##! € käy merimies merta päin.
##! € käy merinainen merelle päin.
##! € kulkee meri-ihminen mereen päin.
SELECT:päin-ADP-after-towards-case PÄIN-ADP (-1* ILL/ALL/PAR BARRIER vblex) ;

##! € on hyvä olo ollut siitä lähtien.
SELECT:lähtien-after-from-case LÄHTIEN-ADP (-1* LOCATIVE-FROM BARRIER vblex) ;

##! € avun tarpeessa
REMOVE:apu-over-avu AVU-NOUN (0 AVU-NOUN) ;

#
# selected words from original Karlsson's cg-1 end here }}}
SECTION # {{{ grammar and context

##! € en oo sitä nähny.
##! € en tule.
##! € ole hiljaa.
IFF:conneg-when-NOUNeg conneg (-1* neg BARRIER conneg) ;
SELECT:neg-when-conneg neg (1* conneg BARRIER vblex) ;
##! € poliisilla on ollut etsintäatehtävä.
##! € katoamisilmoitus on levinnyt sosiaalisessa mediassa.
SELECT:perfect-tense pp (-1* COPULA BARRIER VERBAL) ;

##! €
SELECT:adp-gen-complements ADPS-WITH-GEN (0* gen BARRIER adp) ;
SELECT:adp-par-complements ADPS-WITH-PAR (0* par BARRIER adp) ;
SELECT:adp-ill-complements ADPS-WITH-ILL (0* ill BARRIER adp) ;
SELECT:adp-ela-complements ADPS-WITH-ELA (0* ela BARRIER adp) ;

##! omorfi det is ~pron dem
##! €
REMOVE:det-needs-NP det (NEGATE 1 NOMINAL) ;

##! € siitä on puoli vuotta.
SELECT:nump-partitive NUM-MULTIPLIER (1 par) ;

##! $ vuonna 2015 kirjattiin sata viharikosta
SELECT:measurep-partitive par (-1 num) ;

##! € pääministerit tapaavat Intiassa.
SELECT:verb-pl3-agree-left VBLEX-PL3 (-1 n + pl) ;

##! omorfi infminen is limited to cop´+ tekeminen c.f. Iso suomen kielioppi
##! $ ei ole lahjahevosta suuhin katsomineb
IFF:ei-ole-infminen infminen (-1* OLE-CONNEG) ;

##! agent pcp must be poss or with possessor nearby
##! otherwise it's always infinitive or not ambiguous
##! € kenen piirtämään tauluun me kirjoitetaan?
REMOVE:gen-agent-pcp AGENT-PCP (NEGATE -1 gen) ;

##! basic agreement
SELECT:adj-par-agreement ADJ-PAR (1 par) ;
SELECT:adj-ine-agreement ADJ-INE (1 ine) ;
SELECT:adj-ela-agreement ADJ-ELA (1 ela) ;
SELECT:adj-ill-agreement ADJ-ILL (1 ill) ;
SELECT:adj-ade-agreement ADJ-ADE (1 ade) ;
SELECT:adj-abl-agreement ADJ-ABL (1 abl) ;
SELECT:adj-all-agreement ADJ-ALL (1 all) ;
SELECT:adj-tra-agreement ADJ-TRA (1 tra) ;
SELECT:adj-abe-agreement ADJ-ABE (1 abe) ;
SELECT:adj-ess-agreement ADJ-ESS (1 ess) ;


# }}}
AFTER-SECTIONS # {{{ last-resorts

## This section is a sledgehammer for all things even slightly rare
## and suspicious

##! $ mitä jää jäljelle.
##! $ se on kerrassaan mahtavaa.
##! $ mennään suoraan sinne.
##! € aseta kortit suoraan riviin

##! € olen nähnyt tarpeeksi.

##! € olen todella viisas.
##! $ kerää tarkalleen kolme omenaa.
##! $ se on varsin totta.
##! $ joskus menee myöhään.
##! € he tanssivat myöhään yöhön.
##! $ tavallaan olet kuitenkin oikeassa.
##! € hän teki kaiken omalla tyhmällä tavallaan.
##! $ kirjoitin kaikki säännöt ihan turhaan.
##! € kulutin rahani turhaan roskaan.
##! $ panin kaikki rahani varmaan hevoseen.
##! € varmaan tuhlasin kaikki rahani siihen,
##! $ he ottivat välittömästi yhteen.
##! € panin kaikki rahani yhteen hevoseen.
##! $ yritä uudestaan.
##! $ yritä uudelleen.
##! $ voitele molemmin puolin.
##! $ he joivat vähän vettä.
##! $ he joivat paljon vettä.
##! $ he joivat enemmän vettä.
SELECT:adv-unless-ADJ-ine-agreement ADV-LIKE-ADJ-INE (NEGATE 1 ine) ;
SELECT:adv-unless-ADJ-ill-agreement ADV-LIKE-ADJ-ILL (NEGATE 1 ill) ;
SELECT:adv-unless-ADJ-ins-agreement ADV-LIKE-ADJ-INS (NEGATE 1 ins) ;
SELECT:adv-unless-ADJ-ade-agreement ADV-LIKE-ADJ-ADE (NEGATE 1 ade) ;
SELECT:adv-unless-ADJ-all-agreement ADV-LIKE-ADJ-ALL (NEGATE 1 all) ;
SELECT:adv-unless-ADJ-ela-agreement ADV-LIKE-ADJ-ELA (NEGATE 1 ela) ;
SELECT:adv-unless-ADJ-gen-agreement ADV-LIKE-ADJ-GEN (NEGATE 1 gen) ;
SELECT:adv-unless-noun-ine-agreement ADV-LIKE-NOUN-INE (NEGATE -1 ine) ;
SELECT:adv-unless-noun-tra-agreement ADV-LIKE-NOUN-TRA (NEGATE -1 tra) ;
SELECT:adv-unless-noun-ins-agreement ADV-LIKE-NOUN-INS (NEGATE -1 ins) ;
SELECT:adv-unless-noun-ade-agreement ADV-LIKE-NOUN-ADE (NEGATE -1 ade) ;
SELECT:adv-unless-noun-all-agreement ADV-LIKE-NOUN-ALL (NEGATE -1 all) ;
SELECT:adv-unless-noun-ela-agreement ADV-LIKE-NOUN-ELA (NEGATE -1 ela) ;
##! $ ei ole lainkaan vaikeaa lakata.
##! $ sade oli kastellut hänet läpeensä.
##! $ mistä olet kotoisin?
SELECT:adverbs-with-rarer-NOUNominals DEFAULT-ADV ;
REMOVE:adjectives-rarely-possessed adj + POSS ;
REMOVE:unusual-plurals NOUNS-RARELY-PLURAL ;
REMOVE:rare-verb-shadows-commons RARER-VBLEX ;
REMOVE:rare-propn-shadows-commons RARER-NP ;
REMOVE:rare-noun-shadows-commons RARER-NOUN ;
REMOVE:rare-ADP-shadows-commons RARER-ADP ;
REMOVE:rare-ADV-shadows-commons RARER-ADV ;
SELECT:prefer-punct-over-sym punct (0 punct);
REMOVE:prefer-standard NONSTANDARDS ;
REMOVE:avoid-instructives ins (NOT 0 vblex + infe) ;
REMOVE:avoid-abessives abe (NOT 0 vblex + infma) ;
REMOVE:avoid-clitics CLITIC ;
REMOVE:blacklists BLACKLISTED ;
REMOVE:rare-pers-PRON-forms pl (0 PERS-PRON-SG) ;
REMOVE:prefer-pri-sg3-over-imp-sg2 IMP-SG2 (0 INDV-SG3) ;

SELECT:joka-over-joki JOISSA-PRON (0 JOISSA-NOUN) ;
##! $ heitä pidetään viisaina
REMOVE:pitää-over-pidetä PIDETÄ-PASS (0 PITÄÄ-PASS) ;
# }}}

# vim: set ft=vislcg3 foldmethod=marker:
