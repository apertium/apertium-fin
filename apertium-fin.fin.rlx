## AUTOMATICALLY GENERATED from omorfi
##2023-09-30T19+02:00

# omorfi.cg3
# Constraint grammar for Finnish
# GNU GPLv3
# Authors: Tommi A Pirinen
# but also remade based on Fred Karlsson's fincg:

##! A note on comments: ##! is used for testing:
##! euro sign for example of remove not applying
##! dollar sign for remove rule firing

##  This program is free software: you can redistribute it and/or modify
##  it under the terms of the GNU General Public License as published by
##  the Free Software Foundation, either version 3 of the License.
##
##  This program is distributed in the hope that it will be useful,
##  but WITHOUT ANY WARRANTY; without even the implied warranty of
##  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
##  GNU General Public License for more details.
##
##  You should have received a copy of the GNU General Public License
##  along with this program.  If not, see <http://www.gnu.org/licenses/>.

# {{{ options
STRICT-TAGS +=
    # Universal POS <http://universaldependencies.org/fi/pos/index.html>
    n
    vblex
    vaux
    adj
    adv
    post
    np
    prn
    punct
    punct
    cnjcoo
    cnjadv
    ij
    num
    det
    x
    # Universal features <http://universaldependencies.org/fi/feat/index.html>
    abe
    abl
    acc
    ade
    all
    ela
    ess
    gen
    ill
    ine
    ins
    lat
    nom
    par
    tra
    Case=Com
    clit_han
    clit_ka
    clit_kaan
    clit_kin
    clit_ko
    clit_pa
    clit_s
    conneg
    com
    pst
    sup
    drv_ja
    drv_inen
    drv_va
    drv_la
    drv_minen
    drv_nen
    drv_sti
    drv_u
    infa
    infe
    infma
    cni
    imp
    ind
    pot
    pl
    pxpl
    pxsg
    sg
    card
    ord
    pcp1
    pcp2
    pcp3
    agent
    pcp_neg
    pp
    pprs
    p0
    p1
    p2
    p3
    px1
    px2
    px3
    neg
    dem
    PronType=Prs
    PronType=Rel
    use_archaic
    use_dialect
    past
    pri
    use_nonstd
    actv
    pasv
    # Omorfi specific features
    drv_maisilla
    lex_sti
    final
    initial
    ?
    lowercased
    uppercased
    titlecased
    detitlecased
    use_blacklist
    use_blacklist
    <GoesWith=Missing->
    <AffixType=Prefix>
    <PropnType=First>
    <PropnType=Last>
    ;
OPTIONS += no-inline-sets ;

DELIMITERS = "<.>" "<!>" "<?>" ;

# }}}

SETS

# {{{ grammar groups
## Nominal poses
LIST NOMINAL = n adj prn np ;
## Clitic particle tags
LIST CLITIC = clit_han clit_ko clit_pa clit_s clit_ka clit_kaan clit_kin ;
## Conjunctions
LIST CONJUNCTION = cnjcoo cnjadv ;
## any vebs
LIST VERBAUX = vblex vaux ;
## copula?
LIST COPULA = ("olla" vaux) ;
##
LIST VERB-PL3 = (vblex pl p3) (vaux pl p3) ;
## ad-adjectives etc.
LIST ADA = adv ;
## AD words
LIST AD = adj adv post ;
## comparatives and superlatives
LIST CMP-SUP = com sup ;
##
LIST POSS = pxsg pxpl px1 px2 px3 ;
## agent participle
LIST AGENT-PCP = agent ;
## case subgroups
LIST LOCATIVE-IN = ine ade ;
LIST LOCATIVE-TO = ill all ;
LIST LOCATIVE-FROM = ela abl ;
LIST ILL/ALL/PAR = ill all par ;
LIST PERS-PRON-SG = (PronType=Prs sg) ;
# ordinals XXX: adj doesn't have ord yet...
LIST NUM-ORD = (num ord) ;
LIST ADJ-ORD = (adj ord) ;
# Infintive a
LIST INFA-SG = (infa sg) ;
LIST INFA-PL = (infa pl) ;
LIST INFA = (infa) ;
# Infinitive ma + ill
LIST INFMAAN = (infa sg ill) ;
# adj cases (and other agreement stuffs)
LIST ADJ-PAR = (adj par) (det par) (prn dem par) (num par) ;
LIST ADJ-INE = (adj ine) (det ine) (prn dem ine) (num ine) ;
LIST ADJ-ELA = (adj ela) (det ela) (prn dem ela) (num ela) ;
LIST ADJ-ILL = (adj ill) (det ill) (prn dem ill) (num ill) ;
LIST ADJ-ADE = (adj ade) (det ade) (prn dem ade) (num ade) ;
LIST ADJ-ABL = (adj abl) (det abl) (prn dem abl) (num abl) ;
LIST ADJ-ALL = (adj all) (det all) (prn dem all) (num all) ;
LIST ADJ-TRA = (adj tra) (det tra) (prn dem tra) (num tra) ;
LIST ADJ-ABE = (adj abe) (det abe) (prn dem abe) (num abe) ;
LIST ADJ-ESS = (adj ess) (det ess) (prn dem ess) (num ess) ;
# inmpv sg2 is systematically indv pres sg3
LIST IMPV-SG2 = (imp p2 sg) ;
LIST INDV-SG3 = (ind p3 sg) ;
# Negation
LIST NEG-AUX = (vaux neg) ;
# Personals
LIST PRS-1SG = (prn p1 sg) ;
LIST PRS-2SG = (prn p2 sg) ;
LIST PRS-3SG = (prn p3 sg) ;
LIST PRS-1PL = (prn p1 pl) ;
LIST PRS-2PL = (prn p2 pl) ;
LIST PRS-3PL = (prn p3 pl) ;
# }}}
# {{{ sentences and clauses
LIST BEGINSENT = >>> ;
LIST ENDSENT = "<.>" "<!>" "<?>" ;
LIST CLB = "<,>" ;
# }}}
# {{{ word groups

## short words that many find problems with
LIST BADEVIL_WORDS =
    ("kui" n) ("tai" n) ("vai" n) ("pai" n)
    ("par" n) ("tule" n) ("kuvi" n) ("kos" n)
    ("mon" n) ("lev" n) ("ka" n)
    ("taka" n) ("edus" n)
    ("he" ij) ("ja" n) ("es" n) ("näin" n)
    ("pistin" n) ("to" n) ("luo" n)
    ("aa" n) ("ee" n) ("ii" n) ("oo" n)
    ("uu" n) ("yy" n) ("ää" n) ("öö" n)
    ("tui" n) ("kapi" n) ("nim" n) ("ma" n)
    ("su" n) ("määri" n) ("muu" ij);

## propns that overlap commoner words
## NB: the large coverage omorfi uses all names in Finnish census so lots
## are foreign, "rare" but over 5 hits in the census, so not non-existent
## also presented are parts of multiword names
## This is only used in last resort removals so there is no harm in listing
## lots of stuff here, it only affects sentence-initial uppercase
## ambiguities
LIST RARER-PROPN =
    ("Vanha" np) ("Mei" np) ("Osa" np) ("Hanska" np)
    ("Osta" np) ("Leikkaa" np) ("Varo" np) ("Tarkki" np)
    ("Onkki" np) ("Ede" np) ("Mietti" np) ("Metsä" np)
    ("Puu" np) ("Koto" np) ("Mörä" np) ("Tuli" np)
    ("Hauska" np) ("Uutinen" np) ("Piha" np) ("Sakko" np)
    ("Ota" np) ("Leikkaa" np) ("Lähdet" np) ("Matka" np)
    ("Lähellä" np) ("Makkara" np) ("Maku" np) ("Suo" np)
    ("Kauha" np) ("Tietty" np) ("Muu" np) ("Sen" np)
    ("Erinomainen" np) ("Allekirjoittanut" np) ("Markkina" np)
    ("Turu" np) ("Muu" np) ("Alku" np) ("Lava" np)
    ("Tila" np) ("Pyhä" np) ("Suora" np) ("He" np)
    ("Sen" np) ("Ainoa" np) ("Viimeinen" np) ("Ole" np)
    ("Jo" np) ("Ole" np) ("Ihana" np) ("Tos" np)
    ("Mies" np) ("As" np) ("Asi" np) ("Viimeinen" np)
    ("Toivo" np) ("Pitkä" np) ("Viimeinen" np)
    ("Lusikka" np) ("Asema" np) ("Mies" np)
    ("Olin" np) ("Tieto" np) ("Erä" np) ("Jyrkänne" np)
    ("Kello" np) ("Kuppi" np) ("Mutta" np) ("Yhtiö" np)
    ("Ka" np) ("Kutsu" np) ("Teos" np) ("Teema" np)
    ("Puute" np) ("Soi" np) ("Hakkuu" np) ("Tu" np)
    ("Vä" np) ("Vähä" np) ("Karta" np) ("Silta" np)
    ("Onni" np) ("Joki" np) ("Muuri" np) ("Vuo" np)
    ("Vuoksi" np) ("Kiva" np) ("Miele" np)
    ("Maakunta" np) ("Pia" np) ("Per" np) ("Olle" np)
    ("Johan" np) ("Aino" np) ("Halunen" np) ("Eni" np)
    ("Seuranen" np) ("Koiranen" np) ("Marilla" np)
    ("Marin" np) ("Sarja" np) ("Sen" np)
    ("Tus" np) ("Ole" np) ("huopa" np)
    ("Huo" np) ("Ku" np) ("Mar" np)
    ("Mo" np) ("On" np) ("Tunne" np)
    ("Mee" np) ("Mi" np) ("Emme" np) ("En" np)
    ("To" np) ("Toa" np) ("Ai" np)
    ("Polve" np) ("Polve" np) ("On" np)
    ("Pi" np) ("Noi" np) ("Eri" np) ("Vuohi" np)
    ("Ämpäri" np) ("Yrittä" np) ("Yhtiö" np)
    ("Ylen" np) ("Viisi" np) ("Viesti" np) 
    ("Vaali" np) ("Tähti" np) ("Tyttö" np)
    ("Turma" np) ("Tarina" prn) ("Taitto" np)
    ("Suuri" np) ("Sama" np) ("Sanoma" np)
    ("Sato" np) ("Saksanen" np) ("Sai" np) 
    ("Saare" np) ("Ritvanen" np) ("Rahat" np) 
    ("Päivä" np) ("Pyhä" np) ("Prinssi" np)
    ("Prinssessa" np) ("Pisimpi" np) ("Pi" np)
    ("Perhe" np) ("Paulista" np) ("Pankki" np)
    ("Paikka" np) ("Osa" np) ("Onnea" np)
    ("Omena" np) ("Nii" np) ("Kansanen" np)
    ("Lähe" np) ("Säästö" np) ("Pian" np) 
    ("Na" np) ("Pola" np) ("Kuva" np)
    ("Ky" np) ("Valkoinen" np) ("Pilvi" np)
    ("Ku" np) ("Huijari" np) ("Venäläinen" np)
    ("Kuo" np) ("Kuolema" np) ("Aiko" np) 
    ("Ranto" np) ("Mere" np) ("Rinna" np)
    ("Eränen" np) ("Meres" np) ("Osa" np)
    ("Aiva" np) ("Poika" np) ("Silla" np)
    ("Tulonen" np) ("Tai" np) ("Aiko" np)
    ("Mi" np) ("Tee" np) ("Joen" np) 
    ("Ni" np) ("Pol" np) ("Hei" np)
    ("Toveri" np) ("Yli" np) ("Myrsky" np)
    ("Vatka" np) ("En" np) ("Kiitos" np)
    ("Viide" np) ("Merelle" np) ("Ku" np) 
    ("Illa" np) ("Naapuri" np) ("Aihe" np)
    ("Kota" np) ("Valon" np) ("Herra" np) ;

## also some cases where a rare noun blocks commoner propn (or other POS)
## often languages, nationalities, tribes
LIST RARER-NOUN =
    ("missouri" n) ("tai" n) ("vai" n)
    ("muutti" n) ("kanna" n) ("kerto" n)
    ("taita" n) ("viisi" n) ("koto" n)
    ("suura" n) ("sai" n) ("saava" n)
    ("pitkänen" n) ("niisi" n) ("valin" n) 
    ("talossa" n) ;
## adverbs that are rarer than adp or noun counterparts. also some
## questionable advs that should've been intjs, and some fixed expressions
LIST RARER-ADV =
    ("kuin" adv) ("kun" adv) ("tai" adv) ("sen" adv)
    ("viisi" adv) ("kuuluu" adv) ("koska" adv)
    ("pitkää" adv) ("sinut" adv) ("jos" adv)
    ("sattuu" adv) ("sitä" adv) ;
## verbs that shadow some noun forms etc.
LIST RARER-VERB =
    ("juuria" vblex) ("eritä" vblex) ("niittää" vblex)
    ("nytkiä" vblex) ("siitä" vblex) ("siittää" vblex)
    ("suomia" vblex) ("suoria" vblex) ("taata" vblex)
    ("vastoa" vblex) ("oieta" vblex) ("niistää" vblex)
    ("paapia" vblex) ("entää" vblex) ;
## some adjectives that shadow probably a commoner noun
LIST RARER-ADJ =
    ("ehta" adj) ;
## adverbs that are much commoner than nominal counterparts
LIST DEFAULT-ADV =
    ("erikseen" adv) ("erilleen" adv) ("erillään" adv)
    ("lainkaan" adv) ("läpeensä" adv) ("kaikkiaan" adv)
    ("kumminkaan" adv) ("kumminkin" adv) ("naimisiin" adv)
    ("naimisissa" adv) ("jäljelle" adv) ("kerrassaan" adv)
    ("kotoisin" adv) ("kumminkin" adv) ("kumminkaan" adv)
    ("oikeastaan" adv) ("parhaimmillaan" adv) ("pelkästään" adv)
    ("tietenkin" adv) ("tietenkään" adv) ("toisinaan" adv)
    ("tosiaan" adv) ("usein" adv) ("vahnastaan" adv)
    ("vähintään" adv) ("yhteensä" adv) ("yhtään" adv)
    ("yksinään" adv) ("yksistään" adv) ("öisin" adv)
    ("varsin" adv) ;
## following adverbs are quite safe to select umless matching case is in
## ±1 tokens (depending if ADJ or NOUN ambiguity)
## inessives np initial
LIST ADV-LIKE-ADJ-INE =
    ("yhdessä" adv) ("vinossa" adv) ("pystyssä" adv) ;
## illatives np initial
LIST ADV-LIKE-ADJ-ILL =
    ("suoraan" adv) ("yksiin" adv) ("myöhään" adv)
    ("turhaan" adv) ("varmaan" adv) ("täyteen" adv)
    ("pitkään" adv) ("pystyyn" adv) ;
LIST ADV-LIKE-ADJ-ALL =
    ("uudelleen" adv) ;
LIST ADV-LIKE-ADJ-ELA =
    ("uudestaan" adv) ;
LIST ADV-LIKE-ADJ-ADE =
    ("syvällä" adv) ("samalla" adv) ("pitkällä" adv) ;
LIST ADV-LIKE-ADJ-INS =
    ("myöhemmin" adv) ("nykyisin" adv) ("aiemmin" adv) ;
LIST ADV-LIKE-ADJ-GEN =
    ("vähän" adv) ("paljon" adv) ("vähemmän" adv)
    ("enemmän" adv) ;
## translatives np final
## instructives ... maybe
LIST ADV-LIKE-NOUN-TRA =
    ("tarpeeksi" adv) ("käsiksi" adv) ("mieliksi" adv)
    ("osaksi" adv)  ("periksi" adv) ;
## instructives ... maybe
LIST ADV-LIKE-NOUN-INS =
    ("paikoin" adv) ("selin" adv) ("silloin" adv)
    ("suinkin" adv) ("suinkaan" adv) ("tuskin" adv)
    ("vihdoin" adv) ("puolin" adv) ("muutoin" adv) ;
## adessives
LIST ADV-LIKE-NOUN-ADE =
    ("todella" adv) ("tavallaan" adv) ("mielellään" adv)
    ("perillä" adv) ("suunnitteilla" adv) ("vireillä" adv)
    ("jäljellä" adv) ;
LIST ADV-LIKE-NOUN-ALL =
    ("perille" adv) ("suunnilleen" adv) ;
LIST ADV-LIKE-NOUN-ELA =
    ("suorastaan" adv) ;
LIST ADV-LIKE-NOUN-INE =
    ("jumissa" adv) ;
## adps that are rarer than adv or noun counterparts
## prefer ADPS-WITH-CASE over RARER-ADP
LIST RARER-ADP =
    ("silloin" post) ("vaikka" post) ("aina" post)
    ("mutta" post) ;
## Adpositions by their complement
## adpositions with partitive complement
LIST ADPS-WITH-PAR =
    ("ylös" post) ("päin" post) ("vastaan" post)
    ("ilman" post) ("kautta" post) ("huolimatta" post)
    ("asti" post) ("sitten" post) ("ennen" post)
    ("varten" post) ("kohden" post) ;
## adositions with genitive complement
LIST ADPS-WITH-GEN =
    ("takana" post) ("mukaan" post) ("vuoksi" post)
    ("alla" post) ("kanssa" post) ("kautta" post)
    ("puolesta" post) ("läpi" post) ("johdolla" post)
    ("sisällä" post) ("perusteella" post) ("sijaan" post)
    ("suhteen" post) ("avulla" post) ("jälkeen" post)
    ("myötä" post) ("edessä" post) ("alle" post)
    ("aikana" post) ("joukossa" post) ("alapuolella" post)
    ("lisäksi" post) ("alussa" post) ("päälle" post)
    ("ohi" post) ("mukana" post) ;
## adpositions with illative complement
LIST ADPS-WITH-ILL = ("nähden" post) ;
## adpositions with illative complement
LIST ADPS-WITH-ELA = ("ulos" post) ("riippuen" post) 
                     ("alkaen" post) ;
## Verbs with inf complements
## ... infa
LIST VERBS-WITH-INFA =
    ("alkaa" vaux) ("haluta" vaux) ("tahtoa" vaux)
    ("saattaa" vaux) ("aikoa" vaux) ("pitää" vaux)
    ("voida" vaux) ;
## ... inf maan
LIST VERBS-WITH-INFMAAN =
    ("alkaa" vaux) ("päästä" vaux) ("ruveta" vaux)
    ("pystyä" vblex) ("joutua" vaux) ("tulla" vblex) ;
## nouns where plural is rare / theoretical / shadows more common stuff
LIST NOUNS-RARELY-PLURAL =
    ("kota" n pl) ("ikä" n pl)
    ("kirjo" n pl) ("lei" n pl)
    ("bi" n pl) ("noki" n pl)
    ("tau" n pl) ("show" n pl)
    ("häkä" n pl) ("kulku" n pl)
    ("koko" n pl) ;
## numerals that are used nouns too
LIST NUM-MULTIPLIER =
    ("puoli" num) ("sata" num) ("miljoona" num)
    ("pari" num) ;
## temporal adverbs that can be noun forms
LIST TIME-ADV =
    ("maanantaina" adv) ("tiistaina" adv) ("keskiviikkona" adv)
    ("torstaina" adv) ("perjantaina" adv) ("lauantaina" adv)
    ("sunnuntaina" adv) ;
## curse words that go after voi
LIST VITTUSAATANA =
    ("vittu" ij) ("saatana" ij) ("kiitos" ij)
    ("hitto" ij) ("perkele" ij) ("!" punct) ;
## the words where 2nd person singular possessives overlap with e.g. past
## 3rd person singular e.g. "voisi" = {your butter(s), he could},
## "tiesi" = {your roads, he knew} but also: "kuusi" = {your moon(s),
## six}
LIST SG2POSS-CONFUSION =
    ("kuu" pxsg px2)
    ("voi" pxsg px2)
    ("tie" pxsg px2)
    ("tunti" pxsg px2)
    ("lupa" pxsg px2)
    ("rupi" pxsg px2)
    ("hai" pxsg px2)
    ("taju" pxsg px2)
    ("pyy" pxsg px2)
    ("kerä" pxsg px2)
    ("vasta" pxsg px2)
    ("pää" pxsg px2)
    ("harja" pxsg px2)
    ("vuo" pxsg px2)
    ("pää" pxsg px2)
    ("halu" pxsg px2)
    ("tapa" pxsg px2)
    ("tosi" pxsg px2)
    ("pola" pxsg px2 ill)
    ("poli" pxsg px2 ill)
    ("kohta" pxsg px2)
    ("ohja" pxsg px2)
    ("pala" pxsg px2)
    ("tuho" pxsg px2)
    ("mana" pxsg px2) ;
LIST ADJ-RARELY-NOUN = ("kauhea" n) ("komea" n) ;
LIST WHAT-THE-ADJ = ("pelko" adj) ("koko" adj) ;
LIST NUMNOMGENS = ("seitsemän" num) ("kahdeksan" num)
                  ("yhdeksän" num) ("kymmenen" num) ;
## }}}
## {{{ words
## "siitä" = {to conceive, from it}
LIST SIITÄ-VERBS =
    ("siittää" vblex) ("siitä" vblex) ("si’itä" vblex) ;
LIST SIITÄ-DEM =
    ("se" prn) ("se" det) ;
LIST SIITÄ-NOUN = ("sii" n) ;
## "sinun" = yours
LIST SINUN = ("sinä" sg gen) ;
## ja ADV is spoken
LIST JA-ADV = ("ja" adv) ;
## van Houten, etc.
LIST VAN-PROPN =
    ("van" np) ("von" np) ("der" np) ;
## Helsingin = {Helsinki’s, Helsing’s}
LIST HELSING-PROPN = ("Helsing" np) ;
## spoken
LIST ETTÄ-INTJ = ("että" ij) ;
## sekä - että is paired cconj, otherwise että sconj
LIST SEKÄ-CCONJ = ("sekä" cnjcoo) ;
LIST ETTÄ-CCONJ = ("että" cnjcoo) ;
## niin - kuin is paired cconj
LIST NIIN-CCONJ = ("niin" cnjcoo) ;
LIST KUIN-CCONJ = ("kuin" cnjcoo) ;
## kun - taas is paired sconj?
LIST KUN-SCONJ = ("kun" cnjadv) ;
LIST TAAS-SCONJ = ("taas" cnjadv) ;
## niiden {niisi’s, theirs} (I dunno what niisi is)
LIST NIISI-NOUN = ("niisi" n) ;
## ensin {first, some odd verb that doesn't really go with 1sg}
LIST ENTÄÄ-VERB = ("entää" vblex) ;
LIST ENSIN-ADV = ("ensin" adv) ;
## nainen {female, I might marry/fuck}
LIST NAINEN-VERB = ("naida" vblex pot) ;
## vähän = a little either adv or adj gen
LIST VÄHÄN-ADV = ("vähän" adv) ;
LIST VÄHÄN-ADJ = ("vähä" adj gen) ;
## vaikka mitä
LIST VAIKKA-ADV = ("vaikka" adv) ;
LIST ITG = ("mikä" prn) ("kuinka" adv) ;
## häntä {tail, him}
LIST HÄNTÄ-NOUN = ("häntä" n) ;
LIST HÄNTÄ-PRON = ("hän" prn par) ;
## erikseen { separately, with his rounds }
LIST ERIKSEEN-ADV = ("erikseen" adv) ;
LIST ERIKSEEN-NOUN = ("erä" n pl px3) ;
## jälkeen { after, into trace }
LIST JÄLKEEN-NOUN = ("jälki" n ill) ;
LIST JÄLKEEN-AD = ("jälkeen" adv) ("jälkeen" post) ;
# jäljelle { left, onto trace }
LIST JÄLJELLE-NOUN = ("jälki" n all) ;
LIST JÄLJELLE-ADV = ("jälkeen" adv) ;
## uusia = { new, to renew }
LIST UUSI-ADJ = ("uusi" adj) ;
LIST UUSI-VERB = ("uusia" vblex) ;
## Japan is some rarer propn according to ...
LIST JAPANI-PROPN = ("Japani" np) ;
LIST JAPAN-PROPN = ("Japan" np) ;
## ajan { I drive, time, during }
LIST AJAN-NOUN = ("aika" n gen) ;
LIST AJAN-AD = ("ajan" adv) ("ajan" post) ;
## koko = whole
LIST KOKO-ADJ = ("koko" adj) ;
## uudestaan = {again, from their new}
LIST UUDESTAAN-ADJ = ("uusi" adj ela px3) ;
## jäljelle = {left, to a trace}
LIST JÄLJELLE-AD = ("jäljelle" adv) ("jäljelle" post) ;
## ollenkaan = {at all, while being}
LIST OLLENKAAN-ADV = ("ollenkaan" adv) ;
LIST OLLENKAAN-VERB = ("olla" infe clit_kaan) ;
## erään = {of some, to a set}
LIST ERÄÄN-PRON = ("eräs" prn sg gen) ;
## merkitystä = {of importance, from marked}
LIST MERKITYSTÄ-PAR = ("merkitys" n par) ;
## noissa = {in those, in ashes}
LIST NOISSA-PRON = ("nuo" prn ine) ("tuo" prn pl ine) ;
LIST NOISSA-NOUN = ("noki" n sg ine) ;
## peräisin = {originating from, I'd guarantee}
LIST PERÄISIN-ADV = ("peräisin" adv) ;
LIST PERÄISIN-VERB = ("perätä" vblex cni p1 sg) ;
## sekin = {that one too, of cheque}
LIST SEKIN-PRON = ("se" prn clit_kin) ;
LIST LUNASTAA-VERB = ("lunastaa" vblex) ;
##
LIST TUO-PRON = ("tuo" prn) ;
LIST KAIKKI = ("kaikki" prn) ("kaikki" adj) ;
## vastaan {against}
LIST VASTAAN-ADV = ("vastaan" adv) ;
##
LIST ARVOINEN-ADJ = ("arvoinen" adj) ;
LIST KALLIS-ADJ = ("kallis" adj) ;
## pieniä = {make smaller, small}
LIST PIENIÄ-ADJ = ("pieni" adj par) ;
LIST PIENI-ADJ = ("pieni" adj) ;
LIST PIENIÄ-VERB = ("pieniä" vblex) ;
LIST PIENI-VERB = ("pieniä" vblex imp)
                  ("pieniä" vblex past)
                  ("pieniä" vblex conneg) ;
## vaaleissa etc. {elections, blondes}
LIST VAALEISSA-ADJ = ("vaalea" adj pl ine) ;
LIST VAALEISTA-ADJ = ("vaalea" adj pl ela) ;
LIST VAALEIHIN-ADJ = ("vaalea" adj pl ill) ;
LIST VAALEILLA-ADJ = ("vaalea" adj pl ade) ;
##
LIST OTTAA-VERB = ("ottaa" vblex) ;
##
LIST KALLIS-NOUN = ("kallis" n) ;
##
LIST HYVIN-ADA = ("hyvin" adv) ;
LIST LIIAN-ADV = ("liian" adv) ;
##
LIST MITÄ-ADV = ("mitä" adv) ;
##
LIST TÄYSIN-ADA = ("täysin" adv) ;
##
LIST EDELLÄ-ADV = ("edellä" adv) ;
##
LIST ENNEN-ADV = ("ennen" adv) ;
##
LIST IKINÄ-ADV = ("ikinä" adv) ;
##
LIST KAHDEN-ADV = ("kahden" adv) ;
##
LIST KOOLLE-ADV = ("koolle" adv) ;
##
LIST KAUPPA-NOUN = ("kauppa" n) ;
##
LIST KOHTA-ADV = ("kohta" adv) ;
##
LIST KUTSUA-VERB = ("kutsua" vblex) ;
##
LIST LISÄÄ-ADV = ("lisää" adv) ;
##
LIST JOS-ADV = ("jos" adv) ;
##
LIST KAUEMMAKSI-ADV = ("kauemmaksi" adv) ;
LIST KAUEMPAA-ADV = ("kauempaa" adv) ;
LIST KAUEMPANA-ADV = ("kauempana" adv) ;
LIST KAUEMMAKSI-ADJ = ("kauempi" adj tra) ;
LIST KAUEMPAA-ADJ = ("kauempi" adj par) ;
LIST KAUEMPANA-ADJ = ("kauempi" adj ess) ;
LIST LÄHEMMÄKSI-ADV = ("lähemmäksi" adv) ;
LIST LÄHEMPÄÄ-ADV = ("lähempää" adv) ;
LIST LÄHEMPÄNÄ-ADV = ("lähempänä" adv) ;
LIST LÄHEMMÄKSI-ADJ = ("lähempi" adj tra) ;
LIST LÄHEMPÄÄ-ADJ = ("lähempi" adj par) ;
LIST LÄHEMPÄNÄ-ADJ = ("lähempi" adj ess) ;
## näin = {so, I saw}
LIST NÄIN-ADV = ("näin" adv) ;
LIST NÄIN-VERB = ("nähdä" vblex sg p1 past) ;
##
LIST NIIN-ADV = ("niin" adv) ;
##
LIST ESIM = ("esim." n) ("esimerkiks" adv)
    ("esimerkki" n sg tra) ;
##
LIST TAI-CCONJ = ("tai" cnjcoo) ;
##
LIST NÄKÖJÄÄN-ADV = ("näköjään" adv) ;
##
LIST SIKSI-ADV = ("siksi" adv) ;
##
LIST SUOTTA-ADV = ("suotta" adv) ;
##
LIST SYNTYISIN-ADV = ("syntyisin" adv) ;
##
LIST TAKAISIN-ADV = ("takaisin" adv) ;
##
LIST TOISTAMISEEN-ADV = ("toistamiseen" adv) ;
##
LIST MINÄ-PRON = ("minä" prn) ;
##
LIST HEIDÄN-PRON = ("he" prn gen) ("hän" prn pl gen) ;
##
LIST LUJAA-ADV = ("lujaa" adv) ;
##
LIST SUORAAN-ADV = ("suoraan" adv) ;
##
LIST KAPINA-NOM = ("kapina" n nom) ;
## kotia = {home, lappic tent}
LIST KOTA-NOUN = ("kota" n) ;
LIST KOTI-NOUN = ("koti" n) ;
## kuolema = {death, die-er}
LIST KUOLEMA-VERB = ("kuolla" vblex agent) ("kuolla" vblex <Deriv=Ma>) ;
LIST KUOLEMA-NOUN = ("kuolema" n) ;
## olkaa = {shoulder, be imperative (be quiet!)}
LIST OLKAA-NOUN = ("olka" n sg par) ;
LIST OLKAA-VERB = ("olla" vblex imp pl p2) ;
## teillä = {teas, roads, yours}
LIST TEE-PLURAL = ("tee" n pl) ;
LIST TIE-PLURAL = ("tie" n pl) ;
LIST TE-PLURAL = ("te" prn) ("sinä" prn pl) ;
## tilanne = {your space, situation}
LIST TILANNE-NOUN = ("tilanne" n) ;
LIST TILANNE-POSS = ("tila" n pxpl px2) ;
## vanhempi = {elder (parent), older}
LIST VANHEMPI-NOUN = ("vanhempi" n) ;
## hiukan = [a little, a dustknot's}
LIST HIUKAN-NOUN = ("hiukka" n sg gen) ;
## mm = {millimetres, world championship, 2000, among others, a-ha}
LIST MM-NOUN = ("mm" n) ;
## lähtien = {from onwards, by going}
LIST LÄHTIEN-ADP = ("lähtien" post) ;
##
LIST PÄIN-ADP = ("päin" post) ;
## omaa = {own verb / det}
LIST OMAAN-VERB = ("omata" vblex sg p1) ;
LIST OMAAN-ADJ = ("oma" adj ill) ;
LIST OMAA-VERB = ("omata" vblex sg p2) ;
LIST OMAA-ADJ = ("oma" adj par) ;
## voi = {butter, oh!, can, could}
LIST VOI-INTJ = ("voi" ij) ;
LIST VOI-AUX = ("voida" vaux sg p3) ;
## ei = {no, not, none, negative aux, }
LIST EI-INTJ = ("ei" ij) ;
LIST EI-AUX = ("ei" vaux) ;
## tutlittava
LIST TUTKITTAVA-DRV-VA = ("tutkittaa" <Deriv=Va>) ;
LIST TUTKITTAVA-NOUN = ("tutkittava" n) ;
## merkittävä
LIST MERKITTÄVÄ-DRV-VA = ("merkittää" <Deriv=Va>) ;
LIST MERKITTÄVÄ-ADJ = ("merkittävä" adj) ;
## copulas for minen verb chains
LIST OLE-CONNEG = ("olla" vblex conneg) ;
## vaan = {not ... but (CCONJ), not really at all (ADV)}
LIST VAAN-ADV = ("vaan" adv) ;
LIST VAAN-CCONJ = ("vaan" cnjcoo) ;
## pidetään = {are held, are lengthened}
LIST PIDETÄ-PASS = ("pidetä" vblex pasv) ;
LIST PITÄÄ-PASS = ("pitää" vblex pasv) ;
## joissa = {in the rivers, which}
LIST JOISSA-PRON = ("joka" prn pl) ;
LIST JOISSA-NOUN = ("joki" n pl) ;
## apu, avu
LIST AVU-NOUN = ("apu" n) ;
LIST APU-NOUN = ("avu" n) ;
# aa, aina
LIST AINA-ADV = ("aina" adv) ;
LIST AINA-NOUN = ("aa" n pl ess) ;
# löytää, löysä
LIST LÖYSIN-VERB = ("löytää" vblex past) ;
LIST LÖYSIN-NOUNADJ = ("löysä" adj sup)
                      ("löysä" n ins)
                      ("Löysä" np ins) ;
# maa, maittaa
LIST MAITA-NOUN = ("maa" n par pl) ;
LIST MAITA-VERB = ("maittaa" vblex) ;
# vietti, viettää
LIST VIETTI-NOUN = ("vietti" n) ;
LIST VIETTI-VERB = ("viettää" vblex) ;
# päivää, päivätä
LIST PÄIVÄÄ-NOUN = ("päivä" n) ;
LIST PÄIVÄÄ-VERB = ("päivätä" vblex) ;
# muun muassa
LIST MUUN-ADV = ("muun" adv) ("muiden" adv) ;
LIST MUASSA-ADV = ("muassa" adv) ;
# huutaa, huusi
LIST HUUSIN-NOUN = ("huusi" n) ;
LIST HUUSIN-VERB = ("huutaa" vblex) ;
# kokonaan
LIST KOKONAAN-ADV = ("kokonaan" adv) ;
LIST KOKONAAN-NOUN = ("koko" n ess) ;
# ainoastaan
LIST AINOASTAAN-ADV = ("ainoastaan" adv) ;
LIST AINOASTAAN-NOUNADJ = ("ainoa" ela) ;
# yli etc.
LIST YLI-ADV = ("yli" adv) ;
# olla päällä
LIST PÄÄLLÄ-ADV = ("päällä" adv) ;
# kylmää
LIST KYLMÄÄ-VERB = ("kylmätä" vblex) ;
LIST KYLMÄÄ-ADJNOUN = ("kylmä" adj) ("kylmä" n) ;
# }}}
# {{{ secondaries
## we have these tags when analyser doesn't match the case
LIST RECASED = lowercased uppercased titlecased detitlecased ;
## we have some blacklists
LIST BLACKLISTED = use_blacklist use_blacklist ;
## generic weight = approximate log prob of some linear combo
LIST MIN-WEIGHT = <W=MIN> ;
## we count compound boundaries
LIST MIN-COMPOUND = <CMP=MIN> ;
## tags for known wrong or odd stuff
LIST NONSTANDARDS = use_dialect use_nonstd use_archaic ;
## derivations (apart from the near-inflectional)
LIST DERIVATIONS = drv_inen drv_ja drv_u ;
## Missing hyphen from compound
LIST MISSING-HYPHEN = <GoesWith=Missing-> <AffixType=Prefix> ;
## Any other guessers
LIST GUESSED = <Guesser> ;
## 
LIST TITLECASED = "<\p[Lu].+>"r ;
LIST BOS = >>> ;
# }}}

BEFORE-SECTIONS # {{{ preprocess worst stuff away and select options

## removes all analyses that get too many complaints by users
REMOVE:bad-words BADEVIL_WORDS ;

## ensure ordinals only gives one analysis
## (two is needed for different standards / treebanks)
REMOVE:UD-ordinal-is-adjective NUM-ORD (0 ADJ-ORD) ;

## ensure infa short form only gives one analysis
## (two is needed for different standards / treebanks)
REMOVE:infa-is-sg INFA-PL (0 INFA-SG) ;
# }}}
SECTION # {{{ words minimal context

# If someone writes word with initial uppercaase mid-sentence they should
# mean PROPN (those who break this rule get what they deserve ;-)
SELECT:uppercased-midsent-propn np (NOT -1 BOS) (NOT -1 punct) ;

# trad. possessives
# sinun {kuusi, voisi, tiesi}
REMOVE:only-poss-if-pron-sg2 SG2POSS-CONFUSION (NOT -1 SINUN) ;

# surface-form specific disambiguation between few lexemes

##! € ollaan menos sinne ja.
REMOVE:ja-adv JA-ADV (NOT 1 ENDSENT) ;

##! € ai että, voi että.
REMOVE:että-intj ETTÄ-INTJ (NOT -1 ij) ;
##! € oli siellä sekä kaljaa että apinoita.
REMOVE:sekä-että-cconj ETTÄ-CCONJ (NOT -1* SEKÄ-CCONJ) ;
##! € oli siellä niin kaljaa kuin apinoitakin.
REMOVE:niin-kuin-cconj NIIN-CCONJ (NOT 1* KUIN-CCONJ) ;
##! € oli siellä sekä kaljaa että apinoita.
REMOVE:kun-taas-sconj TAAS-SCONJ (NOT -1* KUN-SCONJ) ;
##! € muun muassa pähkinöitä
REMOVE:muun-muassa MUUN-ADV (NOT 1 MUASSA-ADV) ;
##! $ Helsingissä on lunta.
##! € näitkö sen uuden van Helsingin episodin?
REMOVE:Helsing-needs-van HELSING-PROPN (NOT -1 VAN-PROPN) ;

##! $ pulloissa on vanhan näköiset etiketit.
REMOVE:van-needs-PROPN VAN-PROPN (NOT 1 np) ;

##! yli viisi tuhatta
##! sillan yli
SELECT:yli-num YLI-ADV (1 num) ;

##! tietokone on päällä
##! pöydän päällä on tietokone
##! hän puski palloa päällään
REMOVE:on-päällä-adv PÄÄLLÄ-ADV (NOT -1 COPULA) ;

##! $ Japaniin suuntautuva vierailu.
REMOVE:Japan-not-country JAPAN-PROPN (0 JAPANI-PROPN) ;

##! $ nainen on kuin morsian.
REMOVE:nainen-unlikely-potential  NAINEN-VERB (0 n) ;

##! € kun rahaa ei ole, löytää vaikka mitä.
##! $ vaikka rahaa ei ole, löytää sitä.
REMOVE:vaikka-mitä VAIKKA-ADV (NEGATE 1 ITG) ;

##! $ niiden pitää tulla myös.
REMOVE:niisi-niiden NIISI-NOUN (0 prn) ;

##
REMOVE:päivätä-päivää PÄIVÄÄ-VERB (0 PÄIVÄÄ-NOUN) ;

##! $ löysin sinut!
REMOVE:löysä-löytää LÖYSIN-NOUNADJ (0 LÖYSIN-VERB) ;

##! $ useita maita koetelleessa myrskyssä
REMOVE:maa-maittaa MAITA-VERB (0 MAITA-NOUN) ;

##! $ parlamentti vietti minuutin hiljaisuuden
REMOVE:viettää-vietti VIETTI-NOUN (0 VIETTI-VERB) ;

##! kyse ei ole kokonaan uudesta toiminnasta
REMOVE:kokonaan KOKONAAN-NOUN (0 KOKONAAN-ADV) ;
##! ainoastaan putket ja viemärit ovat
REMOVE:ainoastaan AINOASTAAN-NOUNADJ (0 AINOASTAAN-ADV) ;

##! $ siitä en tiedä mitään.
##! € siitä maailma täyteen lapsia.
REMOVE:siittää-siitä SIITÄ-VERBS (0 SIITÄ-DEM) ;
REMOVE:sii-siitä SIITÄ-NOUN (0 SIITÄ-DEM) ;

##! $ ensin syödään sitten vasta mennään.
REMOVE:entää-ensin ENTÄÄ-VERB (0 ENSIN-ADV) ;


##! $ he odottivat häntä.
##! € koira juoksi häntä heiluen.
SELECT:häntä-pron HÄNTÄ-PRON (0 HÄNTÄ-NOUN) ;

##! $ kaupunginvaltuusto hyväksyi liitoksen maanantaina.
SELECT:temprals-prefer-adv TIME-ADV (0 n) ;

##! $ päätetään siitä erikseen.
SELECT:erikseen-adv ERIKSEEN-ADV (0 ERIKSEEN-NOUN);

##! $ aamun jälkeen on yö.
REMOVE:jälkeen-ad JÄLKEEN-NOUN (0 JÄLKEEN-AD) ;
REMOVE:jäljelle-adv JÄLJELLE-NOUN (0 JÄLJELLE-AD) ;

##! $ Intian uusi pääministeri on Narendra Modi.
SELECT:uusi-adj UUSI-ADJ (0 UUSI-VERB) ;

##! $ hän ei ole ollenkaan rehellinen.
SELECT:ollenkaan-adv OLLENKAAN-ADV (0 OLLENKAAN-VERB) ;

##! ulkona oli eilen kylmää
REMOVE:kylmää-verb KYLMÄÄ-VERB (0 KYLMÄÄ-ADJNOUN) ;

##! $ oikeus omaan kehoon
##! $ minä omaan kehoni
SELECT:omaan-adj-ill OMAAN-ADJ (1 ill) ;
##! $ pitää perustella omaa olemassaoloaan
SELECT:omaa-adj-intj OMAA-ADJ (1 par) ;
##! $ voi vittu saatana!
REMOVE:voi-intj-intj VOI-INTJ (NEGATE 1 VITTUSAATANA) ;
##! $ lääkäri voi määrätä lääkkeitä
SELECT:voi-aux-infa VOI-AUX (1* infa BARRIER vblex) ;
##! $ ei vittu saatana!
REMOVE:ei-intj-intj EI-INTJ (NEGATE 1 VITTUSAATANA) ;
##! $ tutkittavan todellisuudentaju näyttäytyy
REMOVE:tutkittava-over-tutkittaa TUTKITTAVA-DRV-VA (0 TUTKITTAVA-NOUN) ;
##! $ aiheesta annettiin ensimmäinen merkittävä päätös
REMOVE:merkittävä-over-merkittää MERKITTÄVÄ-DRV-VA (0 MERKITTÄVÄ-ADJ) ;
##! $ se ei vaan onnistu
SELECT:ei-vaan-adv VAAN-ADV (-1 neg) ;
##! $ ulkopolitiikka ei tehdä pressan johdolla, vaan pressasta huolimatta
SELECT:ei-blah-vaan-cconj VAAN-CCONJ (-2* neg BARRIER BEGINSENT) ;
##! $ heitä pidetään viisaina
REMOVE:pitää-over-pidetä PIDETÄ-PASS (0 PITÄÄ-PASS) ;
##! € jotta voisi keksiä esimerkin
REMOVE:missing-hyphen-compounds MISSING-HYPHEN ;

# }}}
SECTION # {{{ Fred Karlsson's CG1
# I have only selected the ones I can explain and fidn examples of:
# I also skipped:
# * adps that already have case bindings
# * and advs too
# * nouns that have generic poss solution (kuusi, voisi, ...)
# in general a lot of things also relate to the lexicon / analyser used and
# aren't so directly portable.
# ––Flammie

##! $ emme ratkaise pitkän ajan ongelmia.
SELECT:ajan-noun AJAN-NOUN (-1 gen) ;
##! $ auringon pinta muuttuu koko ajan.
SELECT:ajan-noun AJAN-NOUN (-1 KOKO-ADJ) ;

##! $ erään sateen jälkeen olin yksin.
##! € toiseen erään vaihtopelaajien määrää vähennettiin.
SELECT:erään-pron ERÄÄN-PRON (1 gen) ;




##! $ sillä on merkitystä elinkeinoelämässä.
##! € salasanan löydät seinälle merkitystä tekstistä.
SELECT:merkitystä-par MERKITYSTÄ-PAR (NOT 1 ela) ;

##! $ aiotko mennä sinne noissa vaatteissa.
SELECT:noissa-pron NOISSA-PRON (0 NOISSA-NOUN) ;

##! $ mistä se on peräisin.
SELECT:peräisin-adv PERÄISIN-ADV (0 PERÄISIN-VERB) (0* COPULA) ;

##! $ sekin on vielä.
##! € lunastan sen sekin jahka joudan.
SELECT:sekin-pron SEKIN-PRON (NEGATE -1* LUNASTAA-VERB BARRIER vblex) ;


##! hmm?
SELECT:kaikki-tuo TUO-PRON (-1 KAIKKI) (NOT -2 prn) ;

##! $ tankki täyteen bensaa.

##! $ ota vastaan se lahja.
SELECT:ottaa-vastaan-adv VASTAAN-ADV (-1* OTTAA-VERB BARRIER vblex) ;

##! $ minkä arvoinen turkis se oli.
SELECT:gen-arvoinen ARVOINEN-ADJ (-1 gen) ;

##! $ Japani on kallis maa.
SELECT:kallis-adj KALLIS-ADJ (0 KALLIS-NOUN) ;


##! $ pieniä sieviä sieniä.
REMOVE:pieniä-adj PIENIÄ-VERB (0 PIENIÄ-ADJ) ;

##! $ pieni poika on hänen siskonsa.
##! € pieni porkkana keittoa varten.
REMOVE:pieni-adj PIENI-VERB (0 PIENI-ADJ) ;

##! € presidentti Trump voitti vaaleissa.
##! $ vaaleissa munkeissa on enemmän makua.
REMOVE:vaaleissa-adj VAALEISSA-ADJ (NEGATE 1 ine) ;

##! $ pidän vaaleista naisista.
REMOVE:vaaleista-adj VAALEISTA-ADJ (NEGATE 1 ela) ;

##! € presidentti Trump toi vaaleihin markkinoiden tuntua.
REMOVE:vaaleihin-adj VAALEIHIN-ADJ (NEGATE 1 ill) ;

##! € presidentti Trump valittiin demokraattisilla vaaleilla.
REMOVE:vaaleilla-adj VAALEILLA-ADJ (NEGATE 1 ade) ;

##! € se on hyvin suuri.
##! € se meni hyvin nopeasti.
SELECT:hyvin-adv HYVIN-ADA (1 AD) ;

##! € se on liian vanha.
##! $ vim poistaa liian lian.
SELECT:liian-adv LIIAN-ADV (NEGATE 1 gen) ;

##! € mitä suurempi peukalo sitä suurempi pikkurilli.
##! € mitä suurimmissa määrin kyllä.
SELECT:mitä-ada MITÄ-ADV (1 CMP-SUP) ;

##! $ hän on täysin kyvytön.
SELECT:täysin-ada TÄYSIN-ADA (1 AD) ;


##! $ he ovat vielä edellä.
##! € kuningas ei edellä jätkää.
##! € kuka on sinua edellä?
SELECT:edellä-adv EDELLÄ-ADV (NEGATE -1* neg) (NEGATE -1 gen) (NEGATE -1 par) (NEGATE 1 par) ;

##! $
SELECT:ennen-adv-kuin ENNEN-ADV (1 KUIN-CCONJ) ;

##

##! € en olis ikinä uskonu.
SELECT:ikinä-adv IKINÄ-ADV (NEGATE -1 ess) ;

##! € miten paljon aikaa on jäljellä.

##! kahden kauppa on kolmannen korvapuusti
SELECT:kahden-adv KAHDEN-ADV (NEGATE 1 gen) (NEGATE 1 KAUPPA-NOUN) ;



##!
SELECT:jos-kohta-adv KOHTA-ADV (-1 JOS-ADV) ;

##! kutsutaan kokous koolle.
SELECT:kutsua-koolle KOOLLE-ADV (-1* KUTSUA-VERB) ;


##

SELECT:lisää-adv LISÄÄ-ADV (0* par BARRIER CLB) ;

##!
SELECT:kauemmaksi-adv KAUEMMAKSI-ADV (0 KAUEMMAKSI-ADJ) (NEGATE 1 tra) ;
SELECT:kauempaa-adv KAUEMPAA-ADV (0 KAUEMPAA-ADJ) (NEGATE -1 par) ;
SELECT:kauempana-adv KAUEMPANA-ADV (0 KAUEMPANA-ADJ) (NEGATE -1 ess) ;
SELECT:lähemmäksi-adv LÄHEMMÄKSI-ADV (0 LÄHEMMÄKSI-ADJ) (NEGATE 1 tra) ;
SELECT:lähempää-adv LÄHEMPÄÄ-ADV (0 LÄHEMPÄÄ-ADJ) (NEGATE 1 par) ;
SELECT:lähempänä-adv LÄHEMPÄNÄ-ADV (0 LÄHEMPÄNÄ-ADJ) (NEGATE 1 ess) ;






SELECT:niin-tai-näin-adv NÄIN-ADV (-2 NIIN-ADV) (-1 TAI-CCONJ) ;
SELECT:esim-näin-adv NÄIN-ADV (-1 ESIM) ;

SELECT:näköjään-adv NÄKÖJÄÄN-ADV (NEGATE -1 HEIDÄN-PRON) ;












##! € siksi olen niin iloinen.
##! € voin tulla siksi miksi minua luulet.
SELECT:siksi-adv SIKSI-ADV (-1 BEGINSENT) (NEGATE 1 tra) ;





SELECT:suotta-adv SUOTTA-ADV (NEGATE -1 abe) ;



SELECT:syntyisin-adv SYNTYISIN-ADV (NEGATE -1 MINÄ-PRON) ;


SELECT:takaisin-adv TAKAISIN-ADV (NEGATE -1 MINÄ-PRON) ;


SELECT:toistamiseen-adv TOISTAMISEEN-ADV (NEGATE -1 gen) ;










##! $ he joivat vähän vettä.
##! € hän joi sen vähän veden mitä sai.
SELECT:vähän-adv VÄHÄN-ADV (NOT 1 gen) ;




SELECT:lujaa-adv LUJAA-ADV (NEGATE 1 par) ;


# suoraan = {directly, direct, straight (e.g. poker)}
SELECT:suoraan-adv SUORAAN-ADV (NEGATE -1 ill) (NEGATE 1 ill) ;

## !
SELECT:kapina-nom KAPINA-NOM (NEGATE -1 ess) ;

##! $ uudessa kodissa on mukava asua
##! € kai jossain lapissa asutaan vielä kodissa
REMOVE:koti-over-kota KOTA-NOUN (0 KOTI-NOUN) ;

##! $ kuolema korjaa satoa
REMOVE:kuolema-over-kuolla KUOLEMA-VERB (0 KUOLEMA-NOUN) ;

##! $ olkaa niin hyvä ja menkää.
##! $ jokaisella on kaksi olkaa.
REMOVE:olkaa-usually-verb OLKAA-NOUN (0 OLKAA-VERB) ;

##! $ teiden päässä on Rooma.
##! € Sri Lanka on teiden valmistajamaa.
REMOVE:tee-plural-shadows-tie TEE-PLURAL (0 TIE-PLURAL) ;
##! $ teissä on sitä jotain.
##! $ monissa teissä on paljon kofeiinia.
REMOVE:tee-plural-shadows-te TEE-PLURAL (0 TE-PLURAL) ;

##! $ tilanne oli kuumottava.
##! € työntekoon soveltuvat tilanne ovat olemattomat.
REMOVE:tilanne-over-tila TILANNE-POSS (0 TILANNE-NOUN) ;

##! € vanhemmat ovat vanhempainillassa.
##! € vanhepi herrasmies.
SELECT:vahnempi-noun VANHEMPI-NOUN (NEGATE 1 n) (NEGATE 2 n) ;

##! € hiukan malttia on hyvä olla.
REMOVE:hiukan-adv HIUKAN-NOUN (0 adv) ;

##! € jana on 25 mm pitkä.
SELECT:mm-measure-with-nums MM-NOUN (-1 num) ;

##! € käy merimies merta päin.
##! € käy merinainen merelle päin.
##! € kulkee meri-ihminen mereen päin.
SELECT:päin-adp-after-towards-case PÄIN-ADP (-1* ILL/ALL/PAR BARRIER vblex) ;

##! € on hyvä olo ollut siitä lähtien.
SELECT:lähtien-after-from-case LÄHTIEN-ADP (-1* LOCATIVE-FROM BARRIER vblex) ;

##! € avun tarpeessa
REMOVE:apu-over-avu AVU-NOUN (0 AVU-NOUN) ;

##! € aina pitää jaksaa
REMOVE:aina-over-aa AINA-NOUN (0 AINA-ADV) ;

## potentially bad
SELECT:joka-over-joki JOISSA-PRON (0 JOISSA-NOUN) ;

##! $ huusin lujaa.
SELECT:huutaa-over-huusi HUUSIN-VERB (0 HUUSIN-NOUN) ;

#
# selected words from original Karlsson's cg-1 end here }}}
SECTION # {{{ grammar and context

##! € en oo sitä nähny.
##! € en tule.
##! € ole hiljaa.
IFF:conneg-when-neg conneg (-1* neg BARRIER conneg) ;
SELECT:neg-when-conneg NEG-AUX (1* conneg BARRIER vblex) ;
##! € poliisilla on ollut etsintäatehtävä.
##! € katoamisilmoitus on levinnyt sosiaalisessa mediassa.
SELECT:perfect-participle pp (-1* COPULA BARRIER VERBAUX) ;
SELECT:perfect-copula COPULA (1* pp BARRIER VERBAUX) ;

##! €
IFF:adp-gen-complements ADPS-WITH-GEN (0* gen BARRIER post) ;
IFF:adp-par-complements ADPS-WITH-PAR (0* par BARRIER post) ;
IFF:adp-ill-complements ADPS-WITH-ILL (0* ill BARRIER post) ;
IFF:adp-ela-complements ADPS-WITH-ELA (0* ela BARRIER post) ;

##! omorfi det is ~pron dem
##! €
REMOVE:det-needs-np det (NEGATE 1 NOMINAL) ;

##! € siitä on puoli vuotta.
SELECT:nump-partitive NUM-MULTIPLIER (1 par) ;

##! $ vuonna 2015 kirjattiin sata viharikosta
SELECT:measurep-partitive par (-1 num) ;

##! € pääministerit tapaavat Intiassa.
SELECT:verb-pl3-agree-left VERB-PL3 (-1 n + pl) ;
SELECT:verb-pl3-agree-left-coord VERB-PL3 (-1 n) (-2 cnjcoo) (-3 n) ;
SELECT:verb-pl3-agree-left VERB-PL3 (-1 prn + pl) ;

##! omorfi infminen is limited to cop´+ tekeminen c.f. Iso suomen kielioppi
##! $ ei ole lahjahevosta suuhin katsomineb
IFF:ei-ole-infminen drv_maisilla (-1* OLE-CONNEG) ;

##! agent pcp must be poss or with possessor nearby
##! otherwise it's always infinitive or not ambiguous
##! € kenen piirtämään tauluun me kirjoitetaan?
REMOVE:gen-agent-pcp AGENT-PCP (NEGATE -1 gen) ;

##! basic agreement
SELECT:adj-par-agreement ADJ-PAR (1 par) ;
SELECT:adj-ine-agreement ADJ-INE (1 ine) ;
SELECT:adj-ela-agreement ADJ-ELA (1 ela) ;
SELECT:adj-ill-agreement ADJ-ILL (1 ill) ;
SELECT:adj-ade-agreement ADJ-ADE (1 ade) ;
SELECT:adj-abl-agreement ADJ-ABL (1 abl) ;
SELECT:adj-all-agreement ADJ-ALL (1 all) ;
SELECT:adj-tra-agreement ADJ-TRA (1 tra) ;
SELECT:adj-abe-agreement ADJ-ABE (1 abe) ;
SELECT:adj-ess-agreement ADJ-ESS (1 ess) ;

##! $ sanoja "lainattuja sanoja".
SELECT:final-needs-initial final (-2* initial BARRIER final) ;
##! $ sanoja "lainattuja sanoja".
SELECT:initial-needs-final initial (2* final BARRIER initial) ;

##! laiminlyönti voidaan usein todistaa
SELECT:verbs-with-infa VERBS-WITH-INFA (1* INFA BARRIER CLB) ;
SELECT:infa-with-verbs INFA (-1* VERBS-WITH-INFA BARRIER CLB) ;
##! 
SELECT:verbs-with-infma VERBS-WITH-INFMAAN (1* INFMAAN BARRIER CLB) ;
SELECT:infma-with-verbs INFMAAN (-1* VERBS-WITH-INFMAAN BARRIER CLB) ;

##! $ minä unelmoin
SELECT:subject-1sg PRS-1SG (1 vblex + p1 + sg) ;
##! $ sinä tiedät
SELECT:subject-2sg PRS-2SG (1 vblex + p2 + sg) ;
##! $ me näemme
SELECT:subject-1pl PRS-1PL (1 vblex + p1 + pl) ;
##! $ te tiedätte
SELECT:subject-2pl PRS-2PL (1 vblex + p2 + pl) ;
##! $ Alankomaiden suurin kaupunki
REMOVE:superlative-over-instructive ins (0 sup) ;
##! firstname lastname
SELECT:firstname-lastname <PropnType=First> (1 <PropnType=Last>) ;
SELECT:lastname-after-firstname <PropnType=Last> (-1 <PropnType=First>) ;
# }}}
AFTER-SECTIONS # {{{ last-resorts

## This section is a sledgehammer for all things even slightly rare
## and suspicious

REMOVE:no-last-resort-guesses ? ;

##! $ mitä jää jäljelle.
##! $ se on kerrassaan mahtavaa.
##! $ mennään suoraan sinne.
##! € aseta kortit suoraan riviin

##! € olen nähnyt tarpeeksi.

##! € olen todella viisas.
##! $ kerää tarkalleen kolme omenaa.
##! $ se on varsin totta.
##! $ joskus menee myöhään.
##! € he tanssivat myöhään yöhön.
##! $ tavallaan olet kuitenkin oikeassa.
##! € hän teki kaiken omalla tyhmällä tavallaan.
##! $ kirjoitin kaikki säännöt ihan turhaan.
##! € kulutin rahani turhaan roskaan.
##! $ panin kaikki rahani varmaan hevoseen.
##! € varmaan tuhlasin kaikki rahani siihen,
##! $ he ottivat välittömästi yhteen.
##! € panin kaikki rahani yhteen hevoseen.
##! $ yritä uudestaan.
##! $ yritä uudelleen.
##! $ voitele molemmin puolin.
##! $ he joivat vähän vettä.
##! $ he joivat paljon vettä.
##! $ he joivat enemmän vettä.
SELECT:adv-unless-adj-ine-agreement ADV-LIKE-ADJ-INE (NEGATE 1 ine) ;
SELECT:adv-unless-adj-ill-agreement ADV-LIKE-ADJ-ILL (NEGATE 1 ill) ;
SELECT:adv-unless-adj-ins-agreement ADV-LIKE-ADJ-INS (NEGATE 1 ins) ;
SELECT:adv-unless-adj-ade-agreement ADV-LIKE-ADJ-ADE (NEGATE 1 ade) ;
SELECT:adv-unless-adj-all-agreement ADV-LIKE-ADJ-ALL (NEGATE 1 all) ;
SELECT:adv-unless-adj-ela-agreement ADV-LIKE-ADJ-ELA (NEGATE 1 ela) ;
SELECT:adv-unless-adj-gen-agreement ADV-LIKE-ADJ-GEN (NEGATE 1 gen) ;
SELECT:adv-unless-noun-ine-agreement ADV-LIKE-NOUN-INE (NEGATE -1 ine) ;
SELECT:adv-unless-noun-tra-agreement ADV-LIKE-NOUN-TRA (NEGATE -1 tra) ;
SELECT:adv-unless-noun-ins-agreement ADV-LIKE-NOUN-INS (NEGATE -1 ins) ;
SELECT:adv-unless-noun-ade-agreement ADV-LIKE-NOUN-ADE (NEGATE -1 ade) ;
SELECT:adv-unless-noun-all-agreement ADV-LIKE-NOUN-ALL (NEGATE -1 all) ;
SELECT:adv-unless-noun-ela-agreement ADV-LIKE-NOUN-ELA (NEGATE -1 ela) ;
##! $ ei ole lainkaan vaikeaa lakata.
##! $ sade oli kastellut hänet läpeensä.
##! $ mistä olet kotoisin?
SELECT:adverbs-with-rarer-nominals DEFAULT-ADV ;
REMOVE:adjectives-rarely-possessed adj + POSS ;
REMOVE:unusual-plurals NOUNS-RARELY-PLURAL ;
REMOVE:lexicalised-over-derived-sti drv_sti (0 lex_sti) ;
REMOVE:rare-verb-shadows-commons SUB:* RARER-VERB ;
REMOVE:rare-propn-shadows-commons SUB:* RARER-PROPN ;
REMOVE:rare-noun-shadows-commons SUB:* RARER-NOUN ;
REMOVE:rare-adp-shadows-commons SUB:* RARER-ADP ;
REMOVE:rare-adv-shadows-commons SUB:* RARER-ADV ;
REMOVE:rare-adj-shadows-commons SUB:* RARER-ADJ ;
SELECT:prefer-punct-over-sym punct (0 punct);
REMOVE:prefer-standard SUB:* NONSTANDARDS ;
REMOVE:least-derivations SUB:* DERIVATIONS ;
SELECT:least-compounds MIN-COMPOUND ;
SELECT:probably MIN-WEIGHT ;
REMOVE:avoid-instructives ins (NOT 0 vblex + infe) ;
REMOVE:avoid-abessives abe (NOT 0 vblex + infma) ;
REMOVE:avoid-comitatives Case=Com ;
REMOVE:avoid-clitics SUB:* CLITIC ;
REMOVE:blacklists SUB:* BLACKLISTED ;
REMOVE:avoid-sg0 p0 ;
REMOVE:rare-pers-pron-forms pl (0 PERS-PRON-SG) ;
REMOVE:adj-over-drv-va drv_va (0 adj) ;
REMOVE:prefer-pri-sg3-over-imp-sg2 IMPV-SG2 (0 INDV-SG3) ;
REMOVE:prefer-original-case RECASED (NOT -1 BEGINSENT);

REMOVE:odd-adjs WHAT-THE-ADJ ;



# not all lines sent to disambiguator are sentences with a verb but
# if a user inputs verbless sentence they can expect such parses
SELECT:try-to-find-one-verb-per-sentence vblex (NOT 0*C vblex) ;

SELECT:adverb-last-resort adv (-1C vblex) ;


SELECT:numnom-last-resort nom (0 NUMNOMGENS) ;

# }}}

# vim: set ft=vislcg3 foldmethod=marker:
